// ----------------------------------
// RSDK Project: RSDK-GDT
// Script Description: Zip Line Object
// Script Author: Christian Whitehead/Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0  : object.angle
private alias object.value1  : object.handleX
private alias object.value2  : object.handleY
private alias object.value3  : object.speed
private alias object.value4  : object.trackPos
private alias object.value5  : object.originX
private alias object.value6  : object.originY
private alias object.value7  : object.playerNo

private alias object.scale    : object.lastY

// States
private alias 0 : ZIPLINE_STOPPED
private alias 1 : ZIPLINE_MOVE
private alias 2 : ZIPLINE_RETURN

// Player Aliases
private alias object.entityPos		 :	player.entityPos 		// Where the player is on the object list - P1 should be 0, P2 should be 1
private alias object.state			 :	player.state
private alias object.xpos			 :	player.xpos				// Total world-space position (0x10000 == 1.0)
private alias object.ypos			 :	player.ypos
private alias object.yvel			 :	player.yvel
private alias object.speed			 :	player.speed			// Based on world-space (see above)
private alias object.angle			 :	player.angle			// 512-based, not 360 degrees
private alias object.gravity		 :	player.gravity			// GRAVITY_GROUND or GRAVITY_AIR. Not to be confused with player.gravityStrength, see there for more info
private alias object.animation		 :	player.animation
private alias object.collisionTop	 :	player.collisionTop
private alias object.jumpPress	:	player.jumpPress


// ========================
// Events
// ========================

event ObjectUpdate
	object.lastY = object.ypos

	switch object.state
	case ZIPLINE_STOPPED
		object.speed += 2560
		object.trackPos += object.speed
		if object.trackPos > 0
			object.trackPos = 0
			object.speed = 0
		end if
		break

	case ZIPLINE_MOVE
		if object.xpos < object.originX
			object.speed += 5120
		else
			object.speed -= 5120
		end if
		object.trackPos += object.speed

		if object.playerNo > -1
			player.entityPos = object.playerNo
			if player.state != Player_State_Static
				object.state = ZIPLINE_RETURN
				object.playerNo = -1
			end if
			player.entityPos = 0
		end if

		break

	case ZIPLINE_RETURN
		if object.xpos < object.originX
			object.speed += 2560
		else
			object.speed -= 5120
		end if
		object.trackPos += object.speed
		if object.trackPos < 1
			object.state = ZIPLINE_STOPPED
			if object.speed < -65536
				object.speed = -65536
			end if
		end if
		break
	end switch

	temp0 = object.trackPos
	temp0 >>= 8
	temp0 /= 404

	object.angle = 256
	object.angle -= temp0

	temp0 = object.angle
	if temp0 > 256
		temp0 = 256
		object.angle -= 256
		object.angle <<= 3
		object.angle += 256
	end if
	if temp0 < 0
		temp0 = 0
		object.angle <<= 3
		object.angle += 512
	end if

	Cos(object.xpos, temp0)
	object.xpos *= 25856
	object.xpos += object.originX

	Sin(object.ypos, temp0)
	object.ypos *= 53760
	object.ypos += object.originY

	Cos(object.handleX, object.angle)
	object.handleX *= 4608
	object.handleX += object.xpos
	object.handleX &= 4294901760

	Sin(object.handleY, object.angle)
	object.handleY *= 4608
	object.handleY += object.ypos
	object.handleY &= 4294901760

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		temp0 = object.xpos
		temp1 = object.ypos

		object.xpos = object.handleX
		object.ypos = object.handleY

		if player[currentPlayer].state == Player_State_Static
			if object.playerNo == player[currentPlayer].entityPos
				player[currentPlayer].xpos = object.xpos
				temp2 = player[currentPlayer].collisionTop
				temp2 <<= 16
				player[currentPlayer].ypos = object.ypos
				player[currentPlayer].ypos -= temp2
				player[currentPlayer].ypos += 786432

				if player[currentPlayer].jumpPress == true
					player[currentPlayer].angle = 0
					player[currentPlayer].speed = object.speed
					CallFunction(Player_Action_Jump)
					player[currentPlayer].state = Player_State_Air
					PlaySfx(SfxName[Jump], false)
					object.state = ZIPLINE_RETURN
					object.playerNo = -1
					temp2 = temp1
					temp2 -= object.lastY
					temp2 >>= 1
					player[currentPlayer].yvel += temp2
				end if
			end if
		else
			if object.state == ZIPLINE_STOPPED
				if object.trackPos > -1
					BoxCollisionTest(C_TOUCH, object.entityPos, -8, 2, 8, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
					if checkResult == 1
						player[currentPlayer].xpos = object.xpos
						temp2 = player[currentPlayer].collisionTop
						temp2 <<= 16
						player[currentPlayer].ypos = object.ypos
						player[currentPlayer].ypos -= temp2
						player[currentPlayer].ypos += 786432
						player[currentPlayer].state = Player_State_Static
						player[currentPlayer].animation = ANI_HANGING
						object.state = ZIPLINE_MOVE
						object.priority = PRIORITY_ACTIVE
						object.playerNo = player[currentPlayer].entityPos

						if player[currentPlayer].yvel > 0
							object.speed = 81920
							if temp0 < object.originX
								temp2 = player[currentPlayer].yvel
								temp2 >>= 3
							else
								temp2 = player[currentPlayer].yvel
								temp2 >>= 3
								FlipSign(temp2)
							end if
							object.speed += temp2
						else
							object.speed = 81920
						end if

						PlaySfx(SfxName[Catch], false)

					end if
				end if
			end if
		end if

		object.xpos = temp0
		object.ypos = temp1
	next
end event


event ObjectDraw
	DrawSprite(0)

	temp0 = 0
	temp1 = 8
	while temp0 < 3

		Cos(temp2, object.angle)
		temp2 *= temp1
		temp2 <<= 7
		temp2 += object.xpos

		Sin(temp3, object.angle)
		temp3 *= temp1
		temp3 <<= 7
		temp3 += object.ypos

		DrawSpriteXY(0, temp2, temp3)

		temp0++
		temp1 += 8

	loop

	DrawSpriteXY(1, object.handleX, object.handleY)
end event


event ObjectStartup
	LoadSpriteSheet("DDZ/Gimmicks.gif")

	// Zip Line Frames
	SpriteFrame(-4, -4, 8, 8, 35, 150)
	SpriteFrame(-8, -8, 16, 25, 18, 150)

	// Initialise any instances of the object
	foreach (TypeName[ZipLine], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].originX = object[arrayPos0].xpos
		object[arrayPos0].originX += 13238272
		object[arrayPos0].originY = object[arrayPos0].ypos
		object[arrayPos0].playerNo = -1
	next
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("DDZ/Gimmicks.gif")
	SpriteFrame(-4, -4, 8, 8, 35, 150)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
