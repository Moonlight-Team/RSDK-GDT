// ----------------------------------
// RSDK Project: RSDK-GDT
// Script Description: Chompy Fish Object
// Script Author: Klashicola/Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.startPos.y
private alias object.value1 : object.timer

// States
private alias 0 : CHOMPYFISH_SETUP
private alias 1 : CHOMPYFISH_SWING

// Player Aliases
private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom


// ========================
// Function Declarations
// ========================

reserve function Chompy_DebugDraw
reserve function Chompy_DebugSpawn


// ========================
// Function Definitions
// ========================

private function Chompy_DebugDraw
	object.direction = object.propertyValue
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end function


private function Chompy_DebugSpawn
	CreateTempObject(TypeName[Chompy], object.direction, object.xpos, object.ypos)
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case CHOMPYFISH_SETUP
		object.timer++
		if object.timer == 1
			object.timer = 0
			object.startPos.y = object.ypos
			if object.propertyValue == 0
				object.xvel = -0x20000
				object.rotation = 512
			else
				object.xvel = 0x20000
				object.rotation = 0
			end if

			object.yvel = -0x9C000
			object.state = CHOMPYFISH_SWING
			object.priority = PRIORITY_ACTIVE
		end if
		break

	case CHOMPYFISH_SWING
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += 0x3000

		if object.propertyValue == 0
			object.rotation -= 3
		else
			object.rotation += 3
		end if

		if object.yvel > 0x9C000
			object.priority = PRIORITY_BOUNDS
			object.state = CHOMPYFISH_SETUP
			object.ypos = object.startPos.y
			if object.propertyValue == 0
				object.propertyValue = 1
			else
				object.propertyValue = 0
			end if
		end if
		break
		
	end switch

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -14, -14, 14, 14, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
		if checkResult == true
			CallFunction(Player_BadnikBreak)
		end if
	next
end event


event ObjectDraw
	temp0 = object.frame
	temp0 >>= 3
	DrawSpriteFX(temp0, FX_ROTATE, object.xpos, object.ypos)

	object.frame++
	object.frame %= 16
end event


event ObjectStartup
	LoadSpriteSheet("SH/Enemies.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 91)
	SpriteFrame(-16, -16, 32, 31, 34, 91)

	SetTableValue(TypeName[Chompy Fish], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(Chompy_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(Chompy_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
			
		case 0 // direction
			checkResult = object.propertyValue
			object.propertyValue &= 1
			break
			
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
			
		case 0 // direction
			object.propertyValue = editor.variableValue
			object.propertyValue &= 1
			break
			
		end switch
	end if
end event


event RSDKDraw
	object.direction = object.propertyValue
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end event


event RSDKLoad
	LoadSpriteSheet("SH/Enemies.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 91)

	AddEditorVariable("direction")
	SetActiveVariable("direction")
	AddEnumVariable("Left", 0)
	AddEnumVariable("Right", 1)
end event
