// ----------------------------------
// RSDK Project: RSDK-GDT
// Script Description: Helli Bomber Object
// Script Author: Klashicola/Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.destX
private alias object.value1 : object.timer
private alias object.value2 : object.blades

// States
private alias 0 : HELLIBOMBER_STOPPED
private alias 1 : HELLIBOMBER_MOVE
private alias 2 : HELLIBOMBER_WAIT
private alias 3 : HELLIBOMBER_SHOOT

// Player Aliases
private alias object.xpos     : player.xpos
private alias object.ypos     : player.ypos
private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom


// ========================
// Function Declarations
// ========================

reserve function HelliBomber_DebugDraw
reserve function HelliBomber_DebugSpawn


// ========================
// Function Definitions
// ========================

private function HelliBomber_DebugDraw
	DrawSprite(object.direction)
end function


private function HelliBomber_DebugSpawn
	CreateTempObject(TypeName[Helli Bomber], object.direction, object.xpos, object.ypos)
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case HELLIBOMBER_STOPPED
		object.frame = 0
		break

	case HELLIBOMBER_MOVE
		object.frame = 0

		object.xvel = object.destX
		object.xvel -= object.xpos
		object.xvel >>= 5

		temp0 = object.destX
		temp0 >>= 19

		temp1 = object.xpos
		temp1 >>= 19

		if temp0 == temp1
			object.state = HELLIBOMBER_WAIT
			object.timer = 0
		end if

		if object.xvel > 0
			object.direction = FLIP_X
		else
			object.direction = FLIP_NONE
		end if
		break

	case HELLIBOMBER_WAIT
		object.frame = 0

		object.timer++
		if object.timer == 15
			object.state = HELLIBOMBER_SHOOT
		end if
		break

	case HELLIBOMBER_SHOOT
		object.frame = 2

		object.timer++
		if object.timer == 90 // Technically supposed to be 1.5 seconds of delay, but the timer doesn't get reset in HELLIBOMBER_WAIT, so it become 1.25 seconds of delay instead
			CreateTempObject(TypeName[Bullet], 0, object.xpos, object.ypos)
			if object.direction == FLIP_X
				object[tempObjectPos].xpos += 0x80000
			else
				object[tempObjectPos].xpos -= 0x80000
			end if

			object[tempObjectPos].ypos += 0x120000

			object[tempObjectPos].xvel = player[0].xpos
			object[tempObjectPos].xvel -= object[tempObjectPos].xpos
			object[tempObjectPos].xvel /= 48

			object[tempObjectPos].yvel = player[0].ypos
			object[tempObjectPos].yvel -= object[tempObjectPos].ypos
			object[tempObjectPos].yvel /= 48

			if object[tempObjectPos].yvel < 0
				FlipSign(object[tempObjectPos].yvel)
			end if
		end if
		break
		
	end switch

	temp0 = oscillation
	temp0 <<= 2

	Sin(object.yvel, temp0)
	object.yvel <<= 6

	object.xpos += object.xvel
	object.ypos += object.yvel

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		switch object.state
		case HELLIBOMBER_STOPPED
			BoxCollisionTest(C_TOUCH, object.entityPos, -128, -256, 128, 256, currentPlayer, -1, -1, 1, 1)
			if checkResult == true
				object.destX = player[currentPlayer].xpos
				if player[currentPlayer].xpos > object.xpos
					object.destX += 0x600000
				else
					object.destX -= 0x600000
				end if

				object.state = HELLIBOMBER_MOVE
			end if
			break

		case HELLIBOMBER_WAIT
		case HELLIBOMBER_SHOOT
			if object.xpos < player[currentPlayer].xpos
				object.direction = FLIP_X
			else
				object.direction = FLIP_NONE
			end if
			break
			
		end switch

		BoxCollisionTest(C_TOUCH, object.entityPos, -22, -8, 22, 8, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
		if checkResult == true
			CallFunction(Player_BadnikBreak)
		end if
	next
end event


event ObjectDraw
	object.frame += object.direction
	DrawSprite(object.frame)

	temp0 = object.blades
	temp0 >>= 1
	temp0 += 4
	DrawSprite(temp0)

	object.blades++
	object.blades %= 4 // "uhm...how do i make a loop using these again...?" - Geared, probably
end event


event ObjectStartup
	LoadSpriteSheet("SH/Enemies.gif")
	SpriteFrame(-24, -10, 47, 19, 52, 1)
	SpriteFrame(-24, -10, 47, 19, 52, 21)
	SpriteFrame(-24, -10, 30, 29, 52, 59)
	SpriteFrame(-7, -10, 30, 29, 67, 89)
	SpriteFrame(-16, -18, 32, 8, 52, 41)
	SpriteFrame(-16, -18, 32, 8, 52, 50)

	SetTableValue(TypeName[Helli Bomber], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(HelliBomber_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(HelliBomber_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("SH/Enemies.gif")
	SpriteFrame(-24, -10, 47, 19, 52, 1)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
