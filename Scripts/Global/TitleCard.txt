// ----------------------------------
// RSDK Project: Project Virtua
// Script Description: Title Card Object
// Script Author: Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
private alias object.value1 : object.actNamePos
private alias object.value2 : object.zonePos
private alias object.value3 : object.blueRectPos

// States
private alias 0 : TITLECARD_SETUP
private alias 1 : TITLECARD_DELAY
private alias 2 : TITLECARD_ENTERTITLECARD
private alias 3 : TITLECARD_SHOWTITLECARD
private alias 4 : TITLECARD_EXITREDYELLOWRECT
private alias 5 : TITLECARD_EXITBLUERECT
private alias 6 : TITLECARD_STARTSTAGE
private alias 7 : TITLECARD_CREDITSFADEIN
private alias 8 : TITLECARD_FADETOTITLE

// Types
private alias 0 : TITLECARD_TYPE_ACTNUM
private alias 1 : TITLECARD_TYPE_ACT1
private alias 2 : TITLECARD_TYPE_ACT2
private alias 3 : TITLECARD_TYPE_ACT3
private alias 4 : TITLECARD_TYPE_ZONE
private alias 5 : TITLECARD_TYPE_ZONE_MBZ
private alias 25 : TYPE_TITLECARD

// Player Aliases
private alias object.controlMode : player.controlMode
private alias object.jumpPress 	 : player.jumpPress
private alias object.jumpHold 	 : player.jumpHold
private alias object.up 		 : player.up
private alias object.down 		 : player.down
private alias object.left 		 : player.left
private alias object.right 		 : player.right
private alias object.actNamePos 	 : player.timer

// Tracks
private alias 0 : TRACK_STAGE

// ========================
// Static Values
// ========================

// We miss you, Android 8...
public value redribbony = 0

// Stuff, eh
public value TitleCard_showZoneText = 0


// ========================
// Events
// ========================

event ObjectUpdate
	options.touchControls = false
	
	switch object.state
	case TITLECARD_SETUP
		object.alpha = 0xFF
		object.actNamePos = screen.xcenter
		object.actNamePos += 128
		object.zonePos = screen.xcenter
		object.zonePos += 160
		object.blueRectPos = screen.xcenter
		object.blueRectPos += 32
		if credits.screen > 0
			object.state = TITLECARD_CREDITSFADEIN
			object.timer = 272
			stage.state = STAGE_RUNNING
			SetScreenFade(0, 0, 0, 255)
		else
			object.state++
		end if
		break
		
	case TITLECARD_DELAY
		if object.timer < 30
			object.timer++
		else
			object.timer = 0
			PlayMusic(0)
			object.state++
		end if
		break
		
	case TITLECARD_ENTERTITLECARD
		if redribbony > 180
			redribbony = 180
		end if
		if redribbony < 180
			redribbony += 10
		end if
		if object.actNamePos > -0
			object.actNamePos -= 16
			if object.actNamePos < 18
				object.actNamePos = 18
			end if
		end if
		if object.zonePos > 0
			object.zonePos -= 16
			if object.zonePos < 18
				object.actNamePos = 18
			end if
		end if
		if object.zonePos <= 48
			if object.blueRectPos > 20
				object.blueRectPos -= 16
				if object.blueRectPos < 20
					object.blueRectPos = 20
				end if
			else
				object.state++
			end if
		end if
		break
		
	case TITLECARD_SHOWTITLECARD
		if object.timer < 60
			object.timer++
		else
			object.timer = 0
			object.alpha = 248
			stage.state = STAGE_RUNNING
			currentPlayer = 0
			object.state++
		end if
		break
		
	case TITLECARD_EXITREDYELLOWRECT // This name doesn't makes sense anymore since there's no more red and yellow rects LOL
		if object.alpha > 0
			object.alpha -= 8
		else
			object.state++
			currentPlayer = 0
			while currentPlayer < playerCount
				object[currentPlayer].controlMode = 0
				currentPlayer++
			loop
		end if
		break
		
	case TITLECARD_EXITBLUERECT // Same thing with the case above LOL
		stage.timeEnabled = 1
		object.state++
		break
		
	case TITLECARD_STARTSTAGE
		redribbony -= 20
		object.actNamePos += 32
		object.zonePos += 32
		object.blueRectPos += 32
		if object.timer < 20
			object.timer++
		else
			object.timer = 0
			object.type = TypeName[Blank Object]
		end if
		break
		
	case TITLECARD_CREDITSFADEIN
		if object.timer > 0
			object.timer -= 16
			SetScreenFade(0, 0, 0, object.timer)
		else
			currentPlayer = 0
			while currentPlayer < playerCount
				object[currentPlayer].controlMode = 0
				currentPlayer++
			loop
			ResetObjectEntity(object.entityPos, TypeName[Blank Object], 0, 0, 0)
		end if
		break
		
	case TITLECARD_FADETOTITLE
		if object.timer < 300
			object.timer += 5
			if credits.screen == 0
				music.volume -= 2
			end if
		else
			stage.activeList = PRESENTATION_STAGE
			if credits.screen > 0
				stage.listPos = 2
			else
				stage.listPos = 0
				StopMusic()
			end if
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, object.timer)
		break
	end switch
end event


event ObjectDraw
	if object.state < TITLECARD_CREDITSFADEIN
		SetActivePalette(0, 0, screen.ysize)
		DrawRect(0, 0, screen.xsize, screen.ysize, 0, 0, 0, object.alpha) //Black that fades out

		// The red ribbon
		temp0 = screen.xcenter
		temp0 -= 109
		DrawRect(temp0, 0, 12, redribbony, 224, 0, 0, 255)
		temp0 += 12
		DrawRect(temp0, 0, 8, redribbony, 192, 0, 32, 255)
		temp0 += 8
		DrawRect(temp0, 0, 10, redribbony, 128, 0, 0, 255)
		temp0 += 10
		DrawRect(temp0, 0, 8, redribbony, 96, 0, 32, 255)
		temp0 += 8
		DrawRect(temp0, 0, 11, redribbony, 128, 0, 0, 255)
		temp0 += 11
		DrawRect(temp0, 0, 7, redribbony, 192, 0, 32, 255)
		temp0 += 7
		DrawRect(temp0, 0, 12, redribbony, 224, 0, 0, 255)

		// the "Project Virtua" text
		temp0 = screen.xcenter
		temp0 -= 106
		DrawSpriteScreenXY(29, temp0, redribbony)

		// Act name
		temp0 = screen.xcenter
		temp0 += 100
		temp0 += object.actNamePos
		DrawActName(0, temp0, 100, 0, 0, 8, 0)

		//Zone Text
		if screen.xsize <= 320
			temp0 = screen.xcenter
			temp0 += 28
			temp0 += object.zonePos
			DrawSpriteScreenXY(26, temp0, 100)
		else 
			if screen.xsize <= 424
				temp0 = screen.xcenter
				temp0 += 39
				temp0 += object.zonePos
				DrawSpriteScreenXY(26, temp0, 100)
			else 
				temp0 = screen.xcenter
				temp0 += 34
				temp0 += object.zonePos
				DrawSpriteScreenXY(26, temp0, 100)
			end if
		end if

		if screen.ysize > 320
			temp0 = screen.xcenter
			temp0 -= 20
			temp0 += object.blueRectPos
			if stage.actNum < 5
				temp1 = 26
				temp1 += stage.actNum
				if temp1 > 30
					temp1 = 30
				end if
				DrawSpriteScreenXY(temp1, temp0, 102)
			end if
		else 
			temp0 = screen.xcenter
			temp0 -= 26
			temp0 += object.blueRectPos
			temp0 += object.propertyValue

			if stage.actNum < 4
				temp1 = 26
				temp1 += stage.actNum
				if temp1 > 30
					temp1 = 30
				end if
				if stage.actnum == 0
					DrawSpriteScreenXY(30, temp0, 102)
				else
				DrawSpriteScreenXY(temp1, temp0, 102)
				end if


			end if
		end if
	end if
end event


event ObjectStartup
	redribbony = 0
	LoadSpriteSheet("Global/TitleCard.gif")
	
	//Title Card Letters (0-25)
	SpriteFrame(0, -8, 16, 24, 1, 1)		// A
	SpriteFrame(0, -8, 16, 24, 18, 1)		// B
	SpriteFrame(0, -8, 16, 24, 35, 1)		// C
	SpriteFrame(0, -8, 16, 24, 52, 1)		// D
	SpriteFrame(0, -8, 16, 24, 69, 1)		// E
	SpriteFrame(0, -8, 16, 24, 86, 1)		// F
	SpriteFrame(0, -8, 16, 24, 103, 1)		// G
	SpriteFrame(0, -8, 16, 24, 120, 1)		// H
	SpriteFrame(0, -8, 8, 24, 137, 1)		// I
	SpriteFrame(0, -8, 8, 24, 1, 51)		// J
	SpriteFrame(0, -8, 13, 24, 148, 1)		// K
	SpriteFrame(0, -8, 8, 24, 163, 1)		// L
	SpriteFrame(0, -8, 24, 24, 172, 1)		// M
	SpriteFrame(0, -8, 16, 24, 197, 1)		// N
	SpriteFrame(0, -8, 24, 24, 214, 1)		// O
	SpriteFrame(0, -8, 16, 24, 239, 1)		// P
	SpriteFrame(0, -8, 24, 24, 10, 51)		// Q
	SpriteFrame(0, -8, 16, 24, 1, 26)		// R
	SpriteFrame(0, -8, 16, 24, 18, 26)		// S
	SpriteFrame(0, -8, 16, 24, 35, 26)		// T
	SpriteFrame(0, -8, 16, 24, 52, 26)		// U
	SpriteFrame(0, -8, 16, 24, 69, 26)		// V
	SpriteFrame(0, -8, 24, 24, 35, 51)		// W
	SpriteFrame(0, -8, 16, 24, 60, 51)		// X
	SpriteFrame(0, -8, 16, 24, 86, 26)		// Y
	SpriteFrame(0, -8, 16, 24, 103, 26)		// Z
	SpriteFrame(18, 30, 72, 24, 176, 60)	// ZONE
	SpriteFrame(68, 60, 49, 32, 120, 26)	// ACT 1
	SpriteFrame(68, 60, 49, 32, 170, 26)	// ACT 2
	SpriteFrame(-3, -21, 68, 39, 107, 60)    // PROJECT VIRTUA TEXT

	arrayPos0 = 32
	while arrayPos0 < 0x420
		if object[arrayPos0].type == TypeName[Title Card]
			object[arrayPos0].priority = PRIORITY_ALWAYS
			object[arrayPos0].alpha = 0xFF
			object[arrayPos0].drawOrder = 6
			stage.state = STAGE_PAUSED
		end if
		arrayPos0++
	loop
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event