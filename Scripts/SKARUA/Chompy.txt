// ----------------------------------
// RSDK Project: RSDK-GDT
// Script Description: Chompy Fish Object
// Script Author: Geared
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.startPos.y
private alias object.value1 : object.timer

// States
private alias 0 : CHOMPYFISH_SETUP
private alias 1 : CHOMPYFISH_SWING

// Player Aliases
private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom


// ========================
// Function Declarations
// ========================

reserve function Chompy_DebugDraw
reserve function Chompy_DebugSpawn


// ========================
// Function Definitions
// ========================

private function Chompy_DebugDraw
	DrawSprite(0)
end function


private function Chompy_DebugSpawn
	CreateTempObject(TypeName[Chompy], 0, object.xpos, object.ypos)
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case CHOMPYFISH_SETUP
		object.timer++
		if object.timer == 1
			object.timer = 0
			object.startPos.y = object.ypos
			if object.propertyValue == 0
				object.xvel = -0x20000
				object.rotation = 512
			else
				object.xvel = 0x20000
				object.rotation = 0
			end if

			object.yvel = -0x9C000
			object.state = CHOMPYFISH_SWING
			object.priority = PRIORITY_ACTIVE
		end if
		break

	case CHOMPYFISH_SWING
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += 0x3000

		if object.propertyValue == 0
			object.rotation -= 3
		else
			object.rotation += 3
		end if

		if object.yvel > 0x9C000
			object.priority = PRIORITY_BOUNDS
			object.state = CHOMPYFISH_SETUP
			object.ypos = object.startPos.y
			if object.propertyValue == 0
				object.propertyValue = 2
			else
				object.propertyValue = 0
			end if
		end if
		break
		
	end switch

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -14, -14, 14, 14, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
		if checkResult == true
			CallFunction(Player_BadnikBreak)
		end if
	next
end event


event ObjectDraw
	temp0 = object.frame
	temp0 >>= 3
	DrawSpriteFX(temp0, FX_ROTATE, object.xpos, object.ypos)

	object.frame++
	object.frame &= 15
end event


event ObjectStartup
	LoadSpriteSheet("SH/Enemies.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 91)
	SpriteFrame(-16, -16, 32, 31, 34, 91)

	SetTableValue(TypeName[Chompy Fish], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(Chompy_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(Chompy_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("SSZ/Enemies.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 91)

	// Although used by the object, it shouldn't be set by the editor
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
