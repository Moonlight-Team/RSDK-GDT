// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Tilt Platform V Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer

// States
private alias 0 : TILTPLATFORM_V_IDLE
private alias 1 : TILTPLATFORM_V_FLIPPING
private alias 2 : TILTPLATFORM_V_FLIPPED
private alias 3 : TILTPLATFORM_V_REVERT


// ========================
// Function Declarations
// ========================

reserve function TiltPlatformV_DebugDraw
reserve function TiltPlatformV_DebugSpawn


// ========================
// Function Definitions
// ========================

private function TiltPlatformV_DebugDraw
	DrawSprite(0)
end function


private function TiltPlatformV_DebugSpawn
	CreateTempObject(TypeName[Tilt Platform V], 0, object.xpos, object.ypos)
	object[tempObjectPos].direction = object.direction
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case TILTPLATFORM_V_IDLE
		temp0 = oscillation
		temp0 &= 0xF0
		if temp0 == object.propertyValue
			object.priority 		= PRIORITY_ACTIVE
			object.animationTimer 	= 0
			object.frame 			= 1
			object.state 			= TILTPLATFORM_V_FLIPPING
		end if
		break

	case TILTPLATFORM_V_FLIPPING
		object.animationTimer++
		if object.animationTimer == 4
			object.frame 	= 2
			object.timer 	= 74
			object.state 	= TILTPLATFORM_V_FLIPPED
		end if
		break

	case TILTPLATFORM_V_FLIPPED
		object.timer--
		if object.timer == 0
			object.animationTimer 	= 0
			object.frame 			= 1
			object.state 			= TILTPLATFORM_V_REVERT
		end if
		break

	case TILTPLATFORM_V_REVERT
		object.animationTimer++
		if object.animationTimer == 4
			object.frame = 0
			object.state = TILTPLATFORM_V_IDLE
		end if
		break
		
	end switch

	if object.frame == 0
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_SOLID, object.entityPos, -4, -24, 4, 24, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		next
	end if

	if object.outOfBounds == true
		object.frame 	= 0
		object.state 	= TILTPLATFORM_V_IDLE
		object.priority = PRIORITY_BOUNDS
	end if
end event


event ObjectDraw
	DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
end event


event ObjectStartup
	LoadSpriteSheet("SCZ/Objects.gif")
	SpriteFrame(-4, -24, 8, 48, 311, 99)	// Diagonal (Down Left) 	- #0
	SpriteFrame(-16, -16, 32, 32, 320, 108)	// Diagonal (Down Right) 	- #1
	SpriteFrame(-24, -4, 48, 8, 320, 99)	// Flat (H) 				- #2
	SpriteFrame(-16, -16, 32, 32, 353, 108)	// Flat (V)					- #3

	SetTableValue(TypeName[Tilt Platform V], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(TiltPlatformV_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(TiltPlatformV_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("SCZ/Objects.gif")
	SpriteFrame(-4, -24, 8, 48, 311, 99)	// Diagonal (Down Left)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "intervalOffset")
end event
