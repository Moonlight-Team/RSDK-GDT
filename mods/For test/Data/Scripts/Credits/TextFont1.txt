//---------------Sonic CD Text Font 1 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value1 : Object.TargetLine

// Text Info type
private alias 0 : TEXTINFO_TEXTDATA
private alias 1 : TEXTINFO_TEXTSIZE


event ObjectDraw

	// Only show letters 4th and onwards, as the first 3 letters are for identifying the text type
	arrayPos0 = 3

	// Get the length of the line
	GetTextInfo(temp1, MENU_1, TEXTINFO_TEXTSIZE, Object.TargetLine, 0)

	// Taking away 3 for the reason mentioned above...
	temp1 -= 3

	// Base Position to start drawing text at
	temp2 = temp1
	temp2 <<= 18
	FlipSign(temp2)
	temp2 += object.xpos

	// Loop for drawing all the letters
	while temp1 > 0

		// Get the letter at the current text position
		GetTextInfo(temp0, MENU_1, TEXTINFO_TEXTDATA, Object.TargetLine, arrayPos0)

		// Jump to the according special case, if needed for certain exceptions
		switch temp0
		default
			object.frame = 0

			// Translate lowercase letters to Sprite Frames
			if temp0 > 64
				if temp0 < 91
					object.frame = temp0
					object.frame -= 64
				end if
			end if

			// Translate uppercase letters to Sprite Frames
			if temp0 > 96
				if temp0 < 123
					object.frame = temp0
					object.frame -= 96
				end if
			end if

			if object.frame > 0
				// If the letter is valid, draw it
				DrawSpriteXY(object.frame, temp2, object.ypos)
			end if
			break

		case 38
			// Ampersand (&)
			DrawSpriteXY(32, temp2, object.ypos)
			break

		case 39
			// Tick mark (')
			DrawSpriteXY(30, temp2, object.ypos)
			break

		case 44
			// Comma (,)
			DrawSpriteXY(28, temp2, object.ypos)
			break

		case 46
			// Period (.)
			DrawSpriteXY(27, temp2, object.ypos)
			break

		case 47
			// Forward slash (/)
			DrawSpriteXY(31, temp2, object.ypos)
			break

		case 58
			// Colon (;

			DrawSpriteXY(29, temp2, object.ypos)
			break

		case 94
			// Caret (^)
			DrawSpriteXY(36, temp2, object.ypos)
			break

		case 225
			// Accent mark A (Ã¡)
			DrawSpriteXY(33, temp2, object.ypos)
			break

		case 233
			// Accent mark E (Ã©)
			DrawSpriteXY(34, temp2, object.ypos)
			break

		case 243
			// Accent mark O (Ã³)
			DrawSpriteXY(35, temp2, object.ypos)
			break

		end switch

		// Move the cursor to the next letter
		arrayPos0++

		// Decrease the remaining letter count
		temp1--

		// Each letter is 8 pixels apart
		temp2 += 0x80000
	loop

end event


event ObjectStartup
	LoadSpriteSheet("Menu/CreditsText.gif")

	// Setup all the letter Sprite Frames, primarily using loops to automate the process
	SpriteFrame(-3, -9, 8, 16, 37, 35)

	temp1 = 1
	while temp1 < 35
		temp0 = 1
		while temp0 < 127
			SpriteFrame(-3, -6, 8, 16, temp0, temp1)
			temp0 += 9
		loop
		temp1 += 17
	loop

	temp0 = 1
	while temp0 < 37
		SpriteFrame(-3, -6, 8, 16, temp0, temp1)
		temp0 += 9
	loop

	SpriteFrame(-3, -9, 8, 16, 37, 35)
	SpriteFrame(-3, -9, 8, 16, 46, 35)
	SpriteFrame(-3, -9, 8, 16, 55, 35)
	SpriteFrame(-3, -6, 8, 16, 73, 35)

end event


// ========================
// Editor Subs
// ========================

event RSDKDraw
	temp0 = 0
	while temp0 < 4
		DrawSprite(temp0)
		temp0++
	loop
end event


event RSDKLoad
	LoadSpriteSheet("Menu/CreditsText.gif")
	SpriteFrame(0, -6, 8, 16, 46, 18)
	SpriteFrame(9, -6, 8, 16, 37, 1)
	SpriteFrame(18, -6, 8, 16, 82, 18)
	SpriteFrame(27, -6, 8, 16, 46, 18)


end event
