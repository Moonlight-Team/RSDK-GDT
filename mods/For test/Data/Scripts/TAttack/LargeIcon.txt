//-----------------Sonic CD Large Icon Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.propertyValue : Object.Portrait

// MenuControl Aliases
private alias object[0].value0 : MenuControl.Timer
private alias object[0].value5 : MenuControl.SMCurrentButton
private alias object[0].state : MenuControl.State

// States
private alias 0 : LARGEICON_BLANK
private alias 1 : LARGEICON_ENTER_SCREEN
private alias 2 : LARGEICON_LEAVE_SCREEN

// Menu Control States
// *SM = Sub Menu
private alias 7 : MENUCONTROL_SM_CONTROLS
private alias 10 : MENUCONTROL_SM_CONFIRMED
private alias 11 : MENUCONTROL_SM_LOAD_LEVEL
private alias 13 : MENUCONTROL_SM_COMFIRMED_LEAVE

// Property Values
private alias 0 : PALMTREE_PANIC_LARGE_LARGE_ICON
private alias 1 : COLLISION_CHAOS_LARGE_ICON
private alias 2 : TIDAL_TEMPEST_LARGE_ICON
private alias 3 : QUARTZ_QUADRANT_LARGE_ICON
private alias 4 : WACKY_WORKBENCH_LARGE_ICON
private alias 5 : STARDUST_SPEEDWAY_LARGE_ICON
private alias 6 : METRALLIC_MADNESS_LARGE_ICON


event ObjectUpdate
	switch object.state
	case LARGEICON_BLANK
		break
	case LARGEICON_ENTER_SCREEN
		temp0 = screen.xcenter
		temp0 -= 72
		temp0 <<= 16
		if object.xpos < temp0
			object.xpos += 0x100000
			if object.xpos > temp0
				object.xpos = temp0
			end if
		else
			object.state = LARGEICON_BLANK
		end if
		break

	case LARGEICON_LEAVE_SCREEN
		if object.xpos > -0x480000
			object.xpos -= 0x100000
			if object.xpos < -0x480000
				object.xpos = -0x480000
			end if
		else
			object.state = LARGEICON_BLANK
		end if
		break

	end switch
end event


event ObjectDraw
	DrawSprite(15)
	DrawSprite(Object.Portrait)
	temp0 = screen.xcenter
	temp0 -= 72
	temp0 <<= 16
	temp0 -= object.xpos
	temp0 >>= 16
	temp0 += screen.xcenter
	temp0 += 20
	temp1 = temp0
	temp1 += 68

	switch MenuControl.State
	default
		DrawSprite(16)
		DrawSpriteScreenXY(18, temp0, 11)
		DrawSpriteScreenXY(20, temp1, 11)
		break

	case MENUCONTROL_SM_CONTROLS
		switch MenuControl.SMCurrentButton
		default
			DrawSprite(16)
			DrawSpriteScreenXY(18, temp0, 11)
			DrawSpriteScreenXY(20, temp1, 11)
			break

		case 0
			DrawSprite(17)
			DrawSpriteScreenXY(18, temp0, 11)
			DrawSpriteScreenXY(20, temp1, 11)
			break

		case 1
			DrawSprite(16)
			DrawSpriteScreenXY(19, temp0, 11)
			DrawSpriteScreenXY(20, temp1, 11)
			break

		case 2
			DrawSprite(16)
			DrawSpriteScreenXY(18, temp0, 11)
			DrawSpriteScreenXY(21, temp1, 11)
			break

		end switch
		break

	case MENUCONTROL_SM_CONFIRMED
		temp2 = MenuControl.Timer
		temp2 &= 3
		temp2 >>= 1
		if temp2 == 0
			DrawSpriteScreenXY(21, temp1, 11)
		else
			DrawSpriteScreenXY(20, temp1, 11)
		end if
		DrawSprite(16)
		DrawSpriteScreenXY(18, temp0, 11)
		break

	case MENUCONTROL_SM_LOAD_LEVEL
		DrawSprite(16)
		DrawSpriteScreenXY(18, temp0, 11)
		DrawSpriteScreenXY(21, temp1, 11)
		break

	case MENUCONTROL_SM_COMFIRMED_LEAVE
		temp2 = MenuControl.Timer
		temp2 &= 3
		temp2 >>= 1
		if temp2 == 0
			DrawSprite(17)
		else
			DrawSprite(16)
		end if
		DrawSpriteScreenXY(18, temp0, 11)
		DrawSpriteScreenXY(20, temp1, 11)
		break

	end switch
end event


event ObjectStartup
	LoadSpriteSheet("TAttack/TimeAttack.gif")

	SpriteFrame(-64, -72, 128, 144, 0, 0) // #0  - Palmtree Panic Icon
	SpriteFrame(-64, -72, 128, 144, 256, 0) // #1  - Collision Chaos Icon
	SpriteFrame(-64, -72, 128, 144, 384, 0) // #2  - Tidal Tempest Icon
	SpriteFrame(-64, -72, 128, 144, 0, 144) // #3  - Quartz Quadrant Icon
	SpriteFrame(-64, -72, 128, 144, 128, 144) // #4  - Wacky Workbench Icon
	SpriteFrame(-64, -72, 128, 144, 256, 144) // #5  - Stardust Speedway Icon
	SpriteFrame(-64, -72, 128, 144, 384, 144) // #6  - Metallic Madnes Icon
	SpriteFrame(-64, -72, 128, 144, 0, 288) // #7  - Special Stage 1 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 128, 288) // #8  - Special Stage 2 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 256, 288) // #9  - Special Stage 3 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 384, 288) // #10 - Special Stage 4 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 0, 432) // #11 - Special Stage 5 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 128, 432) // #12 - Special Stage 6 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 256, 432) // #13 - Special Stage 7 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 384, 432) // #14 - Sonic The Hedgehog CD	(Unused)
	SpriteFrame(-67, -75, 134, 150, 306, 873) // #15 - Icon Border
	SpriteFrame(-67, -95, 64, 20, 280, 680) // #16 - Exit
	SpriteFrame(-67, -95, 64, 20, 135, 617) // #17 - Exit Highlight
	SpriteFrame(0, 0, 64, 20, 280, 659) // #18 - Next
	SpriteFrame(0, 0, 64, 20, 200, 617) // #19 - Next Highlight
	SpriteFrame(0, 0, 64, 20, 345, 659) // #20 - Go
	SpriteFrame(0, 0, 64, 20, 265, 617) // #21 - Go Highlight
end event

event RSDKDraw
	DrawSprite(15)
	DrawSprite(object.propertyValue)
end event

event RSDKLoad
	LoadSpriteSheet("TAttack/TimeAttack.gif")
	SpriteFrame(-64, -72, 128, 144, 0, 0) // #0  - Palmtree Panic Icon
	SpriteFrame(-64, -72, 128, 144, 256, 0) // #1  - Collision Chaos Icon
	SpriteFrame(-64, -72, 128, 144, 384, 0) // #2  - Tidal Tempest Icon
	SpriteFrame(-64, -72, 128, 144, 0, 144) // #3  - Quartz Quadrant Icon
	SpriteFrame(-64, -72, 128, 144, 128, 144) // #4  - Wacky Workbench Icon
	SpriteFrame(-64, -72, 128, 144, 256, 144) // #5  - Stardust Speedway Icon
	SpriteFrame(-64, -72, 128, 144, 384, 144) // #6  - Metallic Madnes Icon
	SpriteFrame(-64, -72, 128, 144, 0, 288) // #7  - Special Zone 1 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 128, 288) // #8  - Special Zone 2 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 256, 288) // #9  - Special Zone 3 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 384, 288) // #10 - Special Zone 4 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 0, 432) // #11 - Special Zone 5 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 128, 432) // #12 - Special Zone 6 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 256, 432) // #13 - Special Zone 7 Icon	(Unused)
	SpriteFrame(-64, -72, 128, 144, 384, 432) // #14 - Sonic The Hedgehog CD	(Unused)
	SpriteFrame(-67, -75, 134, 150, 306, 873) // #15 - Icon Border
	SpriteFrame(-67, -95, 64, 20, 280, 680) // #16 - Exit
	SpriteFrame(-67, -95, 64, 20, 135, 617) // #17 - Exit Highlight
	SpriteFrame(0, 0, 64, 20, 280, 659) // #18 - Next
	SpriteFrame(0, 0, 64, 20, 200, 617) // #19 - Next Highlight
	SpriteFrame(0, 0, 64, 20, 345, 659) // #20 - Go
	SpriteFrame(0, 0, 64, 20, 265, 617) // #21 - Go Highlight
end event
