//---------------Sonic CD Text Font 3 Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value1 : Object.TargetLine

// Text Info type
private alias 0 : TEXTINFO_TEXTDATA
private alias 1 : TEXTINFO_TEXTSIZE


event ObjectDraw

	// Starting from the third letter because the first 3 aren't actually credits text
	arrayPos0 = 3

	// Get how many letters are in the line
	GetTextInfo(temp1, MENU_1, TEXTINFO_TEXTSIZE, Object.TargetLine, 0)

	// And subtract 3 from that for the resasons stated above
	temp1 -= 3

	// And since the text should be centered, get its position offset now
	temp2 = temp1
	temp2 <<= 18
	FlipSign(temp2)
	temp2 += object.xpos

	// Loop through all the characters in the text line
	while temp1 > 0

		// Get what character's at the target position
		GetTextInfo(temp0, MENU_1, TEXTINFO_TEXTDATA, Object.TargetLine, arrayPos0)

		// If needed, jump to the expecial exception case
		switch temp0
		default
			object.frame = 0

			// 65-90 are lowercase letter IDs, turn them into Sprite Frames
			if temp0 > 64
				if temp0 < 91
					object.frame = temp0
					object.frame -= 64
				end if
			end if

			// 97-122 are uppercase letter IDs, turn them into Sprite Frames
			if temp0 > 96
				if temp0 < 123
					object.frame = temp0
					object.frame -= 96
				end if
			end if

			if object.frame > 0
				// If it's a valid Frame, then draw the Sprite
				DrawSpriteXY(object.frame, temp2, object.ypos)
			end if
			break

		case 38
			// & (Ampersand)
			DrawSpriteXY(32, temp2, object.ypos)
			break

		case 39
			// ' (Tick)
			DrawSpriteXY(30, temp2, object.ypos)
			break

		case 44
			// , (Comma)
			DrawSpriteXY(28, temp2, object.ypos)
			break

		case 46
			// . (Period)
			DrawSpriteXY(27, temp2, object.ypos)
			break

		case 47
			// / (Forward Slash)
			DrawSpriteXY(31, temp2, object.ypos)
			break

		case 58
			//	:	(Colon)
			DrawSpriteXY(29, temp2, object.ypos)
			break

		case 225
			// Ã¡ (Accent mark A)
			DrawSpriteXY(33, temp2, object.ypos)
			break

		case 233
			// Ã© (Accent mark E)
			DrawSpriteXY(34, temp2, object.ypos)
			break

		case 243
			// Ã³ (Accent mark O)
			DrawSpriteXY(35, temp2, object.ypos)
			break

		case 250
			// Ãº (Accent mark U)
			DrawSpriteXY(36, temp2, object.ypos)
			break

		end switch

		// Next character
		arrayPos0++
		temp1--

		// Letters are to be 8 pixels apart
		temp2 += 0x80000
	loop

end event


event ObjectStartup
	LoadSpriteSheet("Menu/CreditsText.gif")

	// Letter Sprite Frames, primarily using loops

	SpriteFrame(-3, -9, 8, 16, 37, 35)

	temp1 = 88
	while temp1 < 110
		temp0 = 1
		while temp0 < 127
			SpriteFrame(-3, -5, 8, 10, temp0, temp1)
			temp0 += 9
		loop
		temp1 += 11
	loop

	temp0 = 1
	while temp0 < 37
		SpriteFrame(-3, -5, 8, 10, temp0, temp1)
		temp0 += 9
	loop

	SpriteFrame(-3, -8, 8, 13, 37, 110)
	SpriteFrame(-3, -8, 8, 13, 46, 110)
	SpriteFrame(-3, -8, 8, 13, 55, 110)
	SpriteFrame(-3, -8, 8, 13, 64, 110)

end event


// ========================
// Editor Subs
// ========================

event RSDKDraw
	temp0 = 0
	while temp0 < 4
		DrawSprite(temp0)
		temp0++
	loop
end event


event RSDKLoad
	LoadSpriteSheet("Menu/CreditsText.gif")
	SpriteFrame(0, -5, 8, 10, 46, 99)
	SpriteFrame(9, -5, 8, 10, 37, 88)
	SpriteFrame(18, -5, 8, 10, 82, 99)
	SpriteFrame(27, -5, 8, 10, 46, 99)


end event
