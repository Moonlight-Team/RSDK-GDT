//----------------Sonic CD Menu Control Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.Timer // Also used for fading
private alias object.value2 : Object.SelectedButton // Only used for Mobile
private alias object.value3 : Object.SavedRound // This is used AFTER you play in time attack
private alias object.value4 : Object.SMYPos // Also used for the XPos of the zone portrait
private alias object.value5 : Object.SMCurrentButton
private alias object.value6 : Object.BeatedRecord
private alias object.value7 : Object.SSUnlocked
private alias object.scale : Object.OffSet

// Large Icon / Special Large Icon Aliases
private alias object[40].state : LargeIcon.State
private alias object[40].xpos : LargeIcon.XPos
private alias object[40].propertyValue : LargeIcon.Portrait

// Time Stuff
private alias object.value1 : Object.Minutes
private alias object.value2 : Object.Seconds
private alias object.value3 : Object.Milliseconds
private alias object.value4 : Object.ZoneLocked

// States
// *SM = Sub Menu
private alias 0 : MENUCONTROL_SETUP
private alias 1 : MENUCONTROL_FADEIN
private alias 2 : MENUCONTROL_CONTROLS
private alias 3 : MENUCONTROL_BLANK
private alias 4 : MENUCONTROL_RETURN_TO_MENU
private alias 5 : MENUCONTROL_ENTER_SM
private alias 6 : MENUCONTROL_CALL_RECORD_MENU
private alias 7 : MENUCONTROL_SM_CONTROLS
private alias 8 : MENUCONTROL_SM_SCROLL_DOWN
private alias 9 : MENUCONTROL_SM_SCROLL_UP
private alias 10 : MENUCONTROL_SM_CONFIRMED
private alias 11 : MENUCONTROL_SM_LOAD_LEVEL
private alias 12 : MENUCONTROL_SM_LEVEL_RETURN
private alias 13 : MENUCONTROL_SM_COMFIRMED_LEAVE
private alias 14 : MENUCONTROL_SM_EXIT
private alias 15 : MENUCONTROL_SM_RECORD_BEATED
private alias 16 : MENUCONTROL_SCROLL_DOWN
private alias 17 : MENUCONTROL_SCROLLING_1
private alias 18 : MENUCONTROL_SCROLL_UP
private alias 19 : MENUCONTROL_SCROLLING_2

// Large Icon State
private alias 1 : LARGEICON_ENTER_SCREEN
private alias 2 : LARGEICON_LEAVE_SCREEN

// Round Icon States
private alias 1 : ROUNDICON_SELECTED_ZONE

// Presentation Stages
private alias 0 : STAGE_P_TITLE
private alias 1 : STAGE_P_MENU

// Players
private alias 0 : PLAYER_SONIC

// Soundtrack
private alias 0 : OST_JP

// Global SFX
private alias 21 : SFX_G_ACHIEVEMENT
private alias 23 : SFX_G_MENUBUTTON
private alias 27 : SFX_G_SELECT

// Stage SFX
private alias 0 : SFX_S_BACKBUTTON

// Engine & Callbacks
private alias 2 : CALLBACK_TATTACK_NOTIFY_ENTER
private alias 3 : CALLBACK_TATTACK_NOTIFY_EXIT

// Engine Messages
private alias 2 : MESSAGE_LOSTFOCUS

// Device Type
private alias 1 : RETRO_MOBILE

// Game Mode Aliases
private alias 2 : MODE_TIMEATTACK

// SaveRAM[39] is how many zones are unlocked for Time Attack

// Function declarations
reserve function MenuControl_IconTransitions
reserve function MenuControl_DefaultRecord
reserve function MenuControl_ZoneUnlockCheck
reserve function MenuControl_TotalRegularRecord
reserve function MenuControl_TotalSpecialRecord


public function MenuControl_IconTransitions
	arrayPos0 = 32

	//Top Half
	temp2 = screen.xcenter
	temp2 <<= 16

	temp0 = -210
	temp1 = -46
	while arrayPos0 < 36
		// Horizontal Movement
		object[arrayPos0].xpos = temp0
		object[arrayPos0].xpos *= Object.OffSet
		object[arrayPos0].xpos >>= 1
		object[arrayPos0].xpos += temp2
		// Vertical Movement
		object[arrayPos0].ypos = temp1
		object[arrayPos0].ypos *= Object.OffSet
		object[arrayPos0].ypos += 0x640000
		temp0 += 140
		arrayPos0++
	loop

	//Bottom Half
	temp0 = -210
	temp1 = 46
	while arrayPos0 < 40
		// Horizontal Movement
		object[arrayPos0].xpos = temp0
		object[arrayPos0].xpos *= Object.OffSet
		object[arrayPos0].xpos >>= 1
		object[arrayPos0].xpos += temp2
		// Vertical Movement
		object[arrayPos0].ypos = temp1
		object[arrayPos0].ypos *= Object.OffSet
		object[arrayPos0].ypos += 0x640000
		temp0 += 140
		arrayPos0++
	loop
end function


public function MenuControl_DefaultRecord
	arrayPos0 = 48 //Act 1 Time Attack Result
	temp0 = 0
	temp1 = 0
	// Checks in all Time Attack levels if there is a record saved
	while temp0 < 21
		temp1 += saveRAM[arrayPos0]
		arrayPos0 += 6
		temp0++
	loop

	// if there are no records, set the default times (5 Minutes per round)
	if temp1 == 0
		arrayPos0 = 48
		temp0 = 0
		while temp0 < 21
			saveRAM[arrayPos0] = 30000
			arrayPos0++
			saveRAM[arrayPos0] = 30000
			arrayPos0++
			saveRAM[arrayPos0] = 30000
			arrayPos0++
			saveRAM[arrayPos0] = 30000
			arrayPos0++
			saveRAM[arrayPos0] = 30000
			arrayPos0++
			saveRAM[arrayPos0] = 30000
			arrayPos0++
			temp0++
		loop
	end if
end function


public function MenuControl_ZoneUnlockCheck
	temp0 = -1
	arrayPos0 = 3 // Save 0 Stage.ListPos
	while arrayPos0 < 40
		temp1 = saveRAM[arrayPos0]
		// In this order, check if your save is post PPZ 3, post MMZ 2 or if it's on a special stage
		if temp1 > 10
			if temp1 > 67
				if temp1 < 80
					temp1 = 7
				else
					temp1 -= 80
					temp1 /= 10
				end if
			else
				temp1 /= 10
			end if
		else
			temp1 = 0
		end if
		if temp1 > temp0
			temp0 = temp1 // Sets the amount of unlocked zones
		end if
		arrayPos0 += 8 // Jump to next save
	loop
	if temp0 > saveRAM[39]
		saveRAM[39] = temp0 // Set the new value if there are new unlocked zones
	else
		temp0 = saveRAM[39] // Otherwise just use the saved value
	end if

	// Unlocks the playable zones
	arrayPos0 = 32
	while temp0 > 0
		Object.ZoneLocked[arrayPos0] = false
		arrayPos0++ // Skips the locking of this zone
		temp0--
	loop

	// Locks the rest of zones
	while arrayPos0 < 39
		Object.ZoneLocked[arrayPos0] = true
		arrayPos0++
	loop
end function


public function MenuControl_TotalRegularRecord

	// Get the times of all zones
	arrayPos0 = 32
	arrayPos1 = 48
	temp1 = 0
	while arrayPos0 < 39
		temp0 = saveRAM[arrayPos1]
		arrayPos1 += 6
		temp0 += saveRAM[arrayPos1]
		arrayPos1 += 6
		temp0 += saveRAM[arrayPos1]
		arrayPos1 += 6
		temp1 += temp0
		Object.Minutes[arrayPos0] = temp0
		Object.Minutes[arrayPos0] /= 6000

		Object.Seconds[arrayPos0] = temp0
		Object.Seconds[arrayPos0] /= 100
		Object.Seconds[arrayPos0] %= 60

		Object.MilliSeconds[arrayPos0] = temp0
		Object.MilliSeconds[arrayPos0] %= 100
		arrayPos0++
	loop

	// Calculate the total time
	Object.Minutes[arrayPos0] = temp1
	Object.Minutes[arrayPos0] /= 6000

	Object.Seconds[arrayPos0] = temp1
	Object.Seconds[arrayPos0] /= 100
	Object.Seconds[arrayPos0] %= 60

	Object.MilliSeconds[arrayPos0] = temp1
	Object.MilliSeconds[arrayPos0] %= 100

	if Object.Minutes[arrayPos0] < 25
	//logro de time attack
	end if

	if Object.Minutes[arrayPos0] < 30
		Object.SSUnlocked = true
	end if

end function


public function MenuControl_TotalSpecialRecord

	// Get the times of all zones
	arrayPos0 = 32
	arrayPos1 = 49
	temp1 = 0
	while arrayPos0 < 39
		temp0 = saveRAM[arrayPos1]
		arrayPos1 += 18
		temp1 += temp0
		Object.Minutes[arrayPos0] = temp0
		Object.Minutes[arrayPos0] /= 6000

		Object.Seconds[arrayPos0] = temp0
		Object.Seconds[arrayPos0] /= 100
		Object.Seconds[arrayPos0] %= 60

		Object.MilliSeconds[arrayPos0] = temp0
		Object.MilliSeconds[arrayPos0] %= 100
		arrayPos0++
	loop

	// Calculate the total time
	Object.Minutes[arrayPos0] = temp1
	Object.Minutes[arrayPos0] /= 6000

	Object.Seconds[arrayPos0] = temp1
	Object.Seconds[arrayPos0] /= 100
	Object.Seconds[arrayPos0] %= 60

	Object.MilliSeconds[arrayPos0] = temp1
	Object.MilliSeconds[arrayPos0] %= 100

	Object.SSUnlocked = true

end function


event ObjectUpdate
	switch object.state
	case MENUCONTROL_SETUP
		// Makes sure nothing gets carried away from the saves
		stage.debugMode = false
		stage.playerListPos = PLAYER_SONIC
		LampPost.Check = 0
		//Good_Future = false
		//Good_Future_Count = 0
		//Good_Future_List = 0
		//SpecialStage.TimeStones = 0
		ReadSaveRAM()

		object.state = MENUCONTROL_FADEIN
		Object.Timer = 384
		Object.OffSet = 0x40000
		SetScreenFade(0, 0, 0, 255)
		Object.SavedRound = -1
		Object.SelectedButton = -1
		Object.SMCurrentButton = -1
		CallFunction(MenuControl_ZoneUnlockCheck)
		if TimeAttack.Round > -1 // Check if you come from playing time attack
			object.state = MENUCONTROL_SM_LEVEL_RETURN

			LargeIcon.Portrait = TimeAttack.Round
			Object.SavedRound = TimeAttack.Round

			LargeIcon.XPos = screen.xcenter
			LargeIcon.XPos -= 72
			LargeIcon.XPos <<= 16

			Object.SMYPos = TimeAttack.Zone
			Object.SMYPos *= 192
			Object.SMYPos += 192

			if TimeAttack.Result > 0
				arrayPos0 = TimeAttack.Round
				arrayPos0 *= 18

				temp0 = TimeAttack.Zone
				temp0 *= 6

				arrayPos0 += temp0
				arrayPos0 += 48
				arrayPos0 += TimeAttack.MenuSection
				if TimeAttack.Result < saveRAM[arrayPos0]
					// 1st Record Beated
					arrayPos1 = arrayPos0
					arrayPos1 += 4
					arrayPos0 += 2
					saveRAM[arrayPos1] = saveRAM[arrayPos0]
					arrayPos1 -= 2
					arrayPos0 -= 2
					saveRAM[arrayPos1] = saveRAM[arrayPos0]
					saveRAM[arrayPos0] = TimeAttack.Result
					Object.BeatedRecord = 1
				else
					arrayPos0 += 2
					if TimeAttack.Result < saveRAM[arrayPos0]
						// 2st Record Beated
						arrayPos1 = arrayPos0
						arrayPos1 += 2
						saveRAM[arrayPos1] = saveRAM[arrayPos0]
						saveRAM[arrayPos0] = TimeAttack.Result
						Object.BeatedRecord = 2
					else
						arrayPos0 += 2
						if TimeAttack.Result < saveRAM[arrayPos0]
							// 3st Record Beated
							saveRAM[arrayPos0] = TimeAttack.Result
							Object.BeatedRecord = 3
						end if
					end if
				end if
			end if
			Object.OffSet = 0x50000
		else
			TimeAttack.MenuSection = 0
		end if

		CallFunction(MenuControl_DefaultRecord)

		if TimeAttack.MenuSection == 0
			CallFunction(MenuControl_TotalRegularRecord)
		else
			CallFunction(MenuControl_TotalSpecialRecord)
			object[40].type = TypeName[S Large Icon]
			object[41].type = TypeName[S Records]
			arrayPos0 = 32
			while arrayPos0 < 40
				object[arrayPos0].type = TypeName[Special Icon]
				arrayPos0++
			loop
		end if
		WriteSaveRAM()

		CallFunction(MenuControl_IconTransitions)
		PlayMusic(0)
		break

	case MENUCONTROL_FADEIN
		if Object.Timer > 0
			Object.Timer -= 8
			Object.OffSet -= 0x1000
		else
			object.state = MENUCONTROL_CONTROLS
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		CallFunction(MenuControl_IconTransitions)
		break

	case MENUCONTROL_CONTROLS
		CheckEqual(Options.PhysicalControls, true)
		if checkResult == true
			arrayPos0 = 32
			arrayPos0 += Object.SavedRound
			checkResult = false
			if object[arrayPos0].state != ROUNDICON_SELECTED_ZONE
				if keyPress[0].left == true
					Object.SavedRound--
					if Object.SavedRound < 0
						Object.SavedRound = 7
					end if
					PlaySfx(SFX_G_MENUBUTTON, false)
				end if

				if keyPress[0].right == true
					Object.SavedRound++
					if Object.SavedRound > 7
						Object.SavedRound = 0
					end if
					PlaySfx(SFX_G_MENUBUTTON, false)
				end if

				if keyPress[0].up == true
					Object.SavedRound -= 4
					if Object.SavedRound < 0
						Object.SavedRound += 8
						if Object.SSUnlocked == true
							object.state = MENUCONTROL_SCROLL_UP
						end if
					end if
					PlaySfx(SFX_G_MENUBUTTON, false)
				end if

				if keyPress[0].down == true
					Object.SavedRound += 4
					if Object.SavedRound > 7
						Object.SavedRound -= 8
						if Object.SSUnlocked == true
							object.state = MENUCONTROL_SCROLL_DOWN
						end if
					end if
					PlaySfx(SFX_G_MENUBUTTON, false)
				end if

				if keyPress[0].start == true
					checkResult = true
				end if

				if keyPress[0].buttonA == true
					checkResult = true
				end if
			end if

			if checkResult == true
				arrayPos0 = 32
				arrayPos0 += Object.SavedRound
				if Object.ZoneLocked[arrayPos0] == 0
					object[arrayPos0].state = ROUNDICON_SELECTED_ZONE
					Object.SMCurrentButton = 0
					switch Object.SavedRound
					case -1
						break
					case 0
						PlaySfx(SFX_G_SELECT, false)
						break
					case 1
						PlaySfx(SFX_G_SELECT, false)
						break
					case 2
						PlaySfx(SFX_G_SELECT, false)
						break
					case 3
						PlaySfx(SFX_G_SELECT, false)
						break
					case 4
						PlaySfx(SFX_G_SELECT, false)
						break
					case 5
						PlaySfx(SFX_G_SELECT, false)
						break
					case 6
						PlaySfx(SFX_G_SELECT, false)
						break
					case 7
						PlaySfx(SFX_G_SELECT, false)
						object.state++
						StopMusic()
						break
					end switch
				end if
			end if
		end if
		if Options.PhysicalControls == true
			CheckTouchRect(0, 0, screen.xsize, screen.ysize)
			if checkResult > -1
				Options.PhysicalControls = false
				Object.SavedRound = -1
			end if
		else
			if Object.SSUnlocked == true
				temp0 = screen.xsize
				temp0 -= 72
				CheckTouchRect(temp0, 192, screen.xsize, screen.ysize)
				if checkResult > -1
					Object.SelectedButton = 8
				else
					CheckTouchRect(0, 0, screen.xsize, screen.ysize)
					if checkResult < 0
						if Object.SelectedButton == 8
							PlaySfx(SFX_G_MENUBUTTON, false)
							object.state = MENUCONTROL_SCROLL_DOWN
							Object.SelectedButton = -1
						end if
					else
						if Object.SelectedButton == 8
							Object.SelectedButton = -1
						end if
					end if
				end if
			end if

			if Object.SelectedButton > -1
				Object.SavedRound = Object.SelectedButton
			else
				Object.SavedRound = -1
			end if

			if keyPress[0].left == true
				Object.SavedRound = 7
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if

			if keyPress[0].right == true
				Object.SavedRound = 0
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if

			if keyPress[0].up == true
				Object.SavedRound = 7
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if

			if keyPress[0].down == true
				Object.SavedRound = 0
				Object.SelectedButton = -1

				Options.PhysicalControls = true
			end if
		end if
		if keyPress[0].buttonB == true
			Object.SavedRound = 7
			arrayPos0 = 32
			arrayPos0 += Object.SavedRound
			//Telling to RoundIcon that we want to leave
			object[arrayPos0].state = ROUNDICON_SELECTED_ZONE
			object.state++

			StopMusic()
			PlaySfx(SFX_G_SELECT, false)
		end if
		break

	case MENUCONTROL_BLANK
		break

	case MENUCONTROL_RETURN_TO_MENU
		if Object.Timer < 384
			Object.Timer += 8
			Object.OffSet += 0x1000
		else
			stage.listPos = STAGE_P_MENU
			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		CallFunction(MenuControl_IconTransitions)
		break

	case MENUCONTROL_ENTER_SM
		if Object.Timer < 256
			Object.Timer += 8
			Object.OffSet += 0x2000
		else
			if TimeAttack.MenuSection == 0
				object[40].type = TypeName[Large Icon]
				object[41].type = TypeName[Records]
			else
				object[40].type = TypeName[S Large Icon]
				object[41].type = TypeName[S Records]
			end if
			LargeIcon.Portrait = Object.SavedRound
			LargeIcon.State = LARGEICON_ENTER_SCREEN

			object[41].drawOrder = 2
			object[42].drawOrder = 2
			object[43].drawOrder = 2

			Object.Timer = 0
			Object.SMYPos = 0

			object.state++
		end if
		CallFunction(MenuControl_IconTransitions)
		break
	case MENUCONTROL_CALL_RECORD_MENU
		if Object.SMYPos < 192
			Object.SMYPos += 8
		else
			object.state++
		end if
		if Options.PhysicalControls == true
			Object.SMCurrentButton = 0
		else
			Object.SMCurrentButton = -1
		end if
		break

	case MENUCONTROL_SM_CONTROLS
		CheckEqual(Options.PhysicalControls, true)
		if checkResult == true
			if TimeAttack.MenuSection == 0
				if keyPress[0].up == true
					object.state = MENUCONTROL_SM_SCROLL_UP
				end if

				if keyPress[0].down == true
					object.state = MENUCONTROL_SM_SCROLL_DOWN
				end if

				if keyPress[0].left == true
					Object.SMCurrentButton--
					if Object.SMCurrentButton < 0
						Object.SMCurrentButton = 2
					end if
				end if

				if keyPress[0].right == true
					Object.SMCurrentButton++
					if Object.SMCurrentButton > 2
						Object.SMCurrentButton = 0
					end if
				end if
			else
				if keyPress[0].left == true
					Object.SMCurrentButton -= 2
					if Object.SMCurrentButton < 0
						Object.SMCurrentButton = 2
					end if
				end if

				if keyPress[0].right == true
					Object.SMCurrentButton += 2
					if Object.SMCurrentButton > 2
						Object.SMCurrentButton = 0
					end if
				end if
			end if

			checkResult = false
			if keyPress[0].start == true
				checkResult = true
			end if

			if keyPress[0].buttonA == true
				checkResult = true
			end if

			if keyPress[0].buttonB == true
// WARNING: Function FUNC_PLAYSTAGESFX does not exist in RSDKv4!
// Arguments: SFX_S_BACKBUTTON, false
				LargeIcon.State = LARGEICON_LEAVE_SCREEN
				object.state = MENUCONTROL_SM_COMFIRMED_LEAVE

				object[41].drawOrder = 8
				object[42].drawOrder = 8
				object[43].drawOrder = 8

				arrayPos0 = Object.SMYPos
				arrayPos0 /= 192
				arrayPos0 += 40
				object[arrayPos0].drawOrder = 2
			end if

			if checkResult == true
				switch Object.SMCurrentButton
				case 0
// WARNING: Function FUNC_PLAYSTAGESFX does not exist in RSDKv4!
// Arguments: SFX_S_BACKBUTTON, false
					LargeIcon.State = LARGEICON_LEAVE_SCREEN
					object.state = MENUCONTROL_SM_COMFIRMED_LEAVE

					object[41].drawOrder = 8
					object[42].drawOrder = 8
					object[43].drawOrder = 8

					arrayPos0 = Object.SMYPos
					arrayPos0 /= 192
					arrayPos0 += 40
					object[arrayPos0].drawOrder = 2
					break

				case 1
					PlaySfx(SFX_G_MENUBUTTON, false)
					object.state = MENUCONTROL_SM_SCROLL_UP
					break

				case 2
					PlaySfx(SFX_G_SELECT, false)
					object.state = MENUCONTROL_SM_CONFIRMED
					StopMusic()
					break

				end switch
			end if
		end if
		if Options.PhysicalControls == true
			CheckTouchRect(0, 0, screen.xsize, screen.ysize)
			if checkResult > -1
				Options.PhysicalControls = false
				Object.SMCurrentButton = -1
			end if
		else
			temp0 = screen.xcenter
			temp0 -= 140
			temp1 = temp0
			temp1 += 66
			CheckTouchRect(temp0, 0, temp1, 40)
			if checkResult > -1
				Object.SMCurrentButton = 0
			else
				CheckTouchRect(0, 0, screen.xsize, screen.ysize)
				if checkResult < 0
					if Object.SMCurrentButton == 0
// WARNING: Function FUNC_PLAYSTAGESFX does not exist in RSDKv4!
// Arguments: SFX_S_BACKBUTTON, false
						LargeIcon.State = LARGEICON_LEAVE_SCREEN
						object.state = MENUCONTROL_SM_COMFIRMED_LEAVE

						object[41].drawOrder = 8
						object[42].drawOrder = 8
						object[43].drawOrder = 8

						arrayPos0 = Object.SMYPos
						arrayPos0 /= 192
						arrayPos0 += 40
						object[arrayPos0].drawOrder = 2
					end if
				else
					if Object.SMCurrentButton == 0
						Object.SMCurrentButton = -1
					end if
				end if
			end if

			if keyPress[0].buttonB == true
// WARNING: Function FUNC_PLAYSTAGESFX does not exist in RSDKv4!
// Arguments: SFX_S_BACKBUTTON, false
				LargeIcon.State = LARGEICON_LEAVE_SCREEN
				object.state = MENUCONTROL_SM_COMFIRMED_LEAVE

				object[41].drawOrder = 8
				object[42].drawOrder = 8
				object[43].drawOrder = 8

				arrayPos0 = Object.SMYPos
				arrayPos0 /= 192
				arrayPos0 += 40
				object[arrayPos0].drawOrder = 2
			end if

			temp0 += 159
			temp1 += 159
			if TimeAttack.MenuSection == 0
				CheckTouchRect(temp0, 0, temp1, 40)
				if checkResult > -1
					Object.SMCurrentButton = 1
				else
					CheckTouchRect(0, 0, screen.xsize, screen.ysize)
					if checkResult < 0
						if Object.SMCurrentButton == 1
							PlaySfx(SFX_G_MENUBUTTON, false)
							object.state = MENUCONTROL_SM_SCROLL_DOWN
						end if
					else
						if Object.SMCurrentButton == 1
							Object.SMCurrentButton = -1
						end if
					end if
				end if
			end if

			temp0 += 68
			temp1 += 68
			CheckTouchRect(temp0, 0, temp1, 40)
			if checkResult > -1
				Object.SMCurrentButton = 2
			else
				CheckTouchRect(0, 0, screen.xsize, screen.ysize)
				if checkResult < 0
					if Object.SMCurrentButton == 2
						PlaySfx(SFX_G_SELECT, false)
						object.state = MENUCONTROL_SM_CONFIRMED
						StopMusic()
					end if
				else
					if Object.SMCurrentButton == 2
						Object.SMCurrentButton = -1
					end if
				end if
			end if

			if keyPress[0].left == true
				Object.SMCurrentButton = 2
				Options.PhysicalControls = true
			end if

			if keyPress[0].right == true
				Object.SMCurrentButton = 0
				Options.PhysicalControls = true
			end if
		end if
		break

	case MENUCONTROL_SM_SCROLL_UP
		if Object.Timer < 192
			Object.Timer += 8
			Object.SMYPos += 8
			if Object.SMYPos > 767
				Object.SMYPos -= 576
			end if
		else
			Object.Timer = 0
			object.state = MENUCONTROL_SM_CONTROLS
			if Options.PhysicalControls == false
				Object.SMCurrentButton = -1
			end if
		end if

		break

	case MENUCONTROL_SM_SCROLL_DOWN
		if Object.Timer < 192
			Object.Timer += 8
			Object.SMYPos -= 8
			if Object.SMYPos < 192
				Object.SMYPos += 576
			end if
		else
			Object.Timer = 0
			object.state = MENUCONTROL_SM_CONTROLS
			if Options.PhysicalControls == false
				Object.SMCurrentButton = -1
			end if
		end if
		break

	case MENUCONTROL_SM_CONFIRMED
		if Object.Timer < 30
			Object.Timer++
		else
			Object.Timer = 0
			object.state++
		end if
		break

	case MENUCONTROL_SM_LOAD_LEVEL
		if Object.Timer < 320
			Object.Timer += 8
		else
			if TimeAttack.MenuSection == 0
				stage.activeList = REGULAR_STAGE
				stage.listPos = LargeIcon.Portrait
				TimeAttack.Round = LargeIcon.Portrait
				stage.listPos *= 10

				temp0 = Object.SMYPos
				temp0 -= 192
				temp0 /= 192

				TimeAttack.Zone = temp0
				temp0 <<= 2
				stage.listPos += temp0
				if temp0 == 8
					stage.listPos++
				end if
			else
				stage.activeList = SPECIAL_STAGE
				stage.listPos = LargeIcon.Portrait
				TimeAttack.Round = LargeIcon.Portrait
				TimeAttack.Zone = 0
			end if

			Player.Lives = 1
			Player.Score = 0
			Options.GameMode = MODE_TIMEATTACK

			LoadStage()
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case MENUCONTROL_SM_LEVEL_RETURN
		if Object.Timer > 0
			Object.Timer -= 8
		else
			TimeAttack.Round = -1
			if Object.BeatedRecord > 0
				PlaySfx(SFX_G_ACHIEVEMENT, false)
				object.state = MENUCONTROL_SM_RECORD_BEATED
				object[0].drawOrder = 4
			else
				object.state = MENUCONTROL_SM_CONTROLS
			end if
		end if
		SetScreenFade(0, 0, 0, Object.Timer)
		break

	case MENUCONTROL_SM_COMFIRMED_LEAVE
		if Object.Timer < 30
			Object.Timer++
		else
			Object.OffSet = 0x50000
			Object.Timer = 0
			object.state++
		end if

		if Object.SMYPos > 0
			Object.SMYPos -= 8
		end if
		break

	case MENUCONTROL_SM_EXIT
		if Object.Timer < 256
			Object.Timer += 8
			Object.OffSet -= 0x2000
		else
			Object.Timer = 0
			object.state = MENUCONTROL_CONTROLS
		end if
		CallFunction(MenuControl_IconTransitions)
		break

	case MENUCONTROL_SM_RECORD_BEATED
		if Object.Timer < 120
			Object.Timer++
		else
			Object.Timer = 0
			object.state = MENUCONTROL_SM_CONTROLS

			Object.BeatedRecord = 0
			object[0].drawOrder = 1
		end if
		break

	case MENUCONTROL_SCROLL_DOWN
		if Object.Timer < 20
			Object.Timer++

			arrayPos0 = 32
			while arrayPos0 < 40
				object[arrayPos0].ypos -= 0xC0000
				arrayPos0++
			loop
		else
			Object.Timer = 0

			arrayPos0 = 32
			if TimeAttack.MenuSection == 0
				while arrayPos0 < 40
					object[arrayPos0].ypos += 0x1E00000
					object[arrayPos0].type = TypeName[Special Icon]
					arrayPos0++
				loop
				TimeAttack.MenuSection = 1
				CallFunction(MenuControl_TotalSpecialRecord)
			else
				while arrayPos0 < 40
					object[arrayPos0].ypos += 0x1E00000
					object[arrayPos0].type = TypeName[Round Icon]
					arrayPos0++
				loop
				TimeAttack.MenuSection = 0
				CallFunction(MenuControl_TotalRegularRecord)
			end if
			object.state++
		end if
		break

	case MENUCONTROL_SCROLLING_1 // Follow-up to MENUCONTROL_SCROLL_DOWN
		if Object.Timer < 20
			Object.Timer++

			arrayPos0 = 32
			while arrayPos0 < 40
				object[arrayPos0].ypos -= 0xC0000
				arrayPos0++
			loop
		else
			Object.Timer = 0
			object.state = MENUCONTROL_CONTROLS
		end if
		break

	case MENUCONTROL_SCROLL_UP
		if Object.Timer < 20
			Object.Timer++

			arrayPos0 = 32
			while arrayPos0 < 40
				object[arrayPos0].ypos += 0xC0000
				arrayPos0++
			loop
		else
			Object.Timer = 0

			arrayPos0 = 32
			if TimeAttack.MenuSection == 0
				while arrayPos0 < 40
					object[arrayPos0].ypos -= 0x1E00000
					object[arrayPos0].type = TypeName[Special Icon]
					arrayPos0++
				loop
				TimeAttack.MenuSection = 1
				CallFunction(MenuControl_TotalSpecialRecord)
			else
				while arrayPos0 < 40
					object[arrayPos0].ypos -= 0x1E00000
					object[arrayPos0].type = TypeName[Round Icon]
					arrayPos0++
				loop
				TimeAttack.MenuSection = 0
				CallFunction(MenuControl_TotalRegularRecord)
			end if
			object.state++
		end if
		break

	case MENUCONTROL_SCROLLING_2 // Follow-up to MENUCONTROL_SCROLL_UP
		if Object.Timer < 20
			Object.Timer++

			arrayPos0 = 32
			while arrayPos0 < 40
				object[arrayPos0].ypos += 0xC0000
				arrayPos0++
			loop
		else
			Object.Timer = 0
			object.state = MENUCONTROL_CONTROLS
		end if
		break

	end switch
end event


event ObjectDraw
	switch object.state
	case MENUCONTROL_SM_COMFIRMED_LEAVE
		temp0 = screen.xcenter
		temp0 += 16
		DrawRect(temp0, 31, screen.xcenter, 9, 0, 0, 0, 255)
		DrawRect(temp0, 40, screen.xcenter, 135, 224, 0, 0, 255)
		DrawRect(temp0, 175, screen.xcenter, 38, 0, 0, 96, 255)
		break

	case MENUCONTROL_SM_RECORD_BEATED
		temp0 = Object.Timer
		temp0 >>= 2
		temp0 &= 1
		if temp0 == 0
			temp0 = screen.xcenter
			temp0 += 16
			temp1 = Object.BeatedRecord
			temp1 *= 24
			temp1 += 105
			DrawRect(temp0, temp1, 34, 24, 8, 0, 104, 255)
		end if
		break

	end switch
end event


event ObjectStartup
	if Options.Soundtrack == OST_JP
		SetMusicTrack("TimeAttack.ogg", 0, 100512)
	else
		SetMusicTrack("MainMenuSAGE.ogg", 0, 117382)
	end if

	object[0].type = TypeName[Menu Control]
	object[0].drawOrder = 1
	Options.PhysicalControls = false
end event


// ========================
// Editor Subs
// ========================

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143) // #0 - "Script" Icon
end event
