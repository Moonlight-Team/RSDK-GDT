//-----------------Sonic CD Round Icon Script-----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.Timer
private alias object.value1 : Object.Minutes
private alias object.value2 : Object.Seconds
private alias object.value3 : Object.Milliseconds
private alias object.value4 : Object.LockedZone
private alias object.propertyValue : Object.Portrait

// Menu Control Aliases
private alias object[0].value2 : MenuControl.SelectedButton // Only used for Mobile
private alias object[0].value3 : MenuControl.SavedRound // This is used AFTER you play in time attack
private alias object[0].state : MenuControl.State // This is used AFTER you play in time attack

// States
private alias 0 : ROUNDICON_BLANK
private alias 1 : ROUNDICON_SELECTED_ZONE

// Menu Control States
private alias 2 : MENUCONTROL_CONTROLS
private alias 3 : MENUCONTROL_BLANK
private alias 4 : MENUCONTROL_RETURN_TO_MENU
private alias 5 : MENUCONTROL_ENTER_SM

// Global SFX
private alias 27 : SFX_G_SELECT

// Property Values
private alias 0 : PALMTREE_PANIC_ICON
private alias 1 : COLLISION_CHAOS_ICON
private alias 2 : TIDAL_TEMPEST_ICON
private alias 3 : QUARTZ_QUADRANT_ICON
private alias 4 : WACKY_WORKBENCH_ICON
private alias 5 : STARDUST_SPEEDWAY_ICON
private alias 6 : METRALLIC_MADNESS_ICON
private alias 7 : EXIT_ICON

// Face Buffer flags
private alias 1 : FACE_FLAG_TEXTURED_2D


event ObjectUpdate
#platform: Mobile
	if Options.PhysicalControls == false
		if MenuControl.State == MENUCONTROL_CONTROLS
			if Object.LockedZone == false
				temp0 = object.ixpos
				temp0 -= 34
				temp1 = object.ixpos
				temp1 += 34
				temp2 = object.iypos
				temp2 -= 45
				temp3 = object.iypos
				temp3 += 45
				CheckTouchRect(temp0, temp2, temp1, temp3)
				if checkResult > -1
					MenuControl.SelectedButton = Object.Portrait
				else
					CheckTouchRect(0, 0, screen.xsize, screen.ysize)
					if checkResult < 0
						if MenuControl.SelectedButton == Object.Portrait
							PlaySfx(SFX_G_SELECT, false)
							MenuControl.State = MENUCONTROL_BLANK
							object.state = ROUNDICON_SELECTED_ZONE
							if Object.Portrait == EXIT_ICON // Exit button pressed?
								StopMusic()
							end if
						end if
					else
						if MenuControl.SelectedButton == Object.Portrait
							MenuControl.SelectedButton = -1
						end if
					end if
				end if
			end if
		end if
	end if
#endplatform
end event


event ObjectDraw
	DrawSprite(10)
	// Icon Movement
	object.rotation++
	object.rotation &= 511
	Cos(temp0, object.rotation)
	temp0 >>= 4
	Sin(temp1, object.rotation)
	temp1 >>= 4

	switch Object.Portrait
	case PALMTREE_PANIC_ICON
		temp0 += 64
		temp1 += 76
		break

	case COLLISION_CHAOS_ICON
		temp0 += 320
		temp1 += 76
		break

	case TIDAL_TEMPEST_ICON
		temp0 += 448
		temp1 += 76
		break

	case QUARTZ_QUADRANT_ICON
		temp0 += 64
		temp1 += 220
		break

	case WACKY_WORKBENCH_ICON
		temp0 += 192
		temp1 += 220
		break

	case STARDUST_SPEEDWAY_ICON
		temp0 += 320
		temp1 += 220
		break

	case METRALLIC_MADNESS_ICON
		temp0 += 448
		temp1 += 220
		break

	end switch

	if Object.Portrait < EXIT_ICON // Draw for all main stages
		vertexBuffer[0].x = object.xpos
		vertexBuffer[0].x >>= 16
		vertexBuffer[0].x -= 32
		vertexBuffer[0].u = temp0
		vertexBuffer[0].u -= 32
		vertexBuffer[0].y = object.ypos
		vertexBuffer[0].y >>= 16
		vertexBuffer[0].y -= 43
		vertexBuffer[0].v = temp1
		vertexBuffer[0].v -= 43
		vertexBuffer[1].x = object.xpos
		vertexBuffer[1].x >>= 16
		vertexBuffer[1].x += 31
		vertexBuffer[1].u = temp0
		vertexBuffer[1].u += 31
		vertexBuffer[1].y = vertexBuffer[0].y
		vertexBuffer[1].v = vertexBuffer[0].v
		vertexBuffer[2].x = vertexBuffer[0].x
		vertexBuffer[2].u = vertexBuffer[0].u
		vertexBuffer[2].y = object.ypos
		vertexBuffer[2].y >>= 16
		vertexBuffer[2].y += 24
		vertexBuffer[2].v = temp1
		vertexBuffer[2].v += 24
		vertexBuffer[3].x = vertexBuffer[1].x
		vertexBuffer[3].u = vertexBuffer[1].u
		vertexBuffer[3].y = vertexBuffer[2].y
		vertexBuffer[3].v = vertexBuffer[2].v
		Draw3DScene()
		DrawSprite(19)

		temp0 = object.xpos
		temp0 >>= 16
		temp0 -= 24

		temp1 = object.ypos
		temp1 >>= 16
		temp1 += 34
		// Zone specific time
		DrawNumbers(0, temp0, temp1, Object.Minutes, 2, 6, 1)
		temp0 += 16
		DrawNumbers(0, temp0, temp1, Object.Seconds, 2, 6, 1)
		temp0 += 19
		DrawNumbers(0, temp0, temp1, Object.Milliseconds, 2, 6, 1)
	else // Draw the Exit
		DrawSprite(20)
		temp0 = object.xpos
		temp0 >>= 16
		temp0 -= 18
		temp1 = object.ypos
		temp1 >>= 16
		temp1 += 34
		// Total Time
		DrawNumbers(0, temp0, temp1, Object.Minutes, 3, 6, 1)
		temp0 += 16
		DrawNumbers(0, temp0, temp1, Object.Seconds, 2, 6, 1)
		temp0 += 19
		DrawNumbers(0, temp0, temp1, Object.Milliseconds, 2, 6, 1)
		DrawSprite(22)
	end if

	DrawSprite(object.frame)
	if object.state == ROUNDICON_SELECTED_ZONE
		temp0 = Object.Timer
		temp0 &= 3
		temp0 >>= 1
		if temp0 == 0
			if MenuControl.SavedRound == Object.Portrait
				DrawSprite(21)
			end if
		end if

		Object.Timer++
		if Object.Timer == 30
			Object.Timer = 0
			object.state = ROUNDICON_BLANK
			if Object.Portrait == EXIT_ICON
				MenuControl.State = MENUCONTROL_RETURN_TO_MENU
			else
				MenuControl.State = MENUCONTROL_ENTER_SM
				MenuControl.SelectedButton = -1
			end if
		end if
	else
		if MenuControl.State == MENUCONTROL_CONTROLS
			if MenuControl.SavedRound == Object.Portrait
				DrawSprite(21)
			end if
		end if
	end if

	if Object.LockedZone == true
		DrawSprite(23)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("TAttack/TimeAttack.gif")

	SpriteFrame(0, 0, 6, 8, 330, 577) // #0  - 0
	SpriteFrame(0, 0, 6, 8, 337, 577) // #1  - 1
	SpriteFrame(0, 0, 6, 8, 344, 577) // #2  - 2
	SpriteFrame(0, 0, 6, 8, 351, 577) // #3  - 3
	SpriteFrame(0, 0, 6, 8, 330, 586) // #4  - 4
	SpriteFrame(0, 0, 6, 8, 337, 586) // #5  - 5
	SpriteFrame(0, 0, 6, 8, 344, 586) // #6  - 6
	SpriteFrame(0, 0, 6, 8, 351, 586) // #7  - 7
	SpriteFrame(0, 0, 6, 8, 330, 595) // #8  - 8
	SpriteFrame(0, 0, 6, 8, 337, 595) // #9  - 9
	SpriteFrame(-34, -45, 68, 90, 1, 609) // #10 - Border
	SpriteFrame(-32, 15, 64, 16, 70, 577) // #11 - Palmtree Panic
	SpriteFrame(-32, 15, 64, 16, 135, 577) // #12 - Collision Chaos
	SpriteFrame(-32, 15, 64, 16, 200, 577) // #13 - Tidal Tempest
	SpriteFrame(-32, 15, 64, 16, 265, 577) // #14 - Quartz Quadrant
	SpriteFrame(-32, 15, 64, 16, 70, 593) // #15 - Wacky Workbench
	SpriteFrame(-32, 15, 64, 16, 135, 593) // #16 - Stardust Speedway
	SpriteFrame(-32, 15, 64, 16, 200, 593) // #17 - Metallic Madness
	SpriteFrame(-32, 15, 64, 16, 265, 593) // #18 - Total Time
	SpriteFrame(-32, 31, 64, 13, 70, 638) // #19 - ' "
	SpriteFrame(-32, 31, 64, 13, 70, 652) // #20 -  ' "
	SpriteFrame(-35, -46, 70, 92, 441, 860) // #21 - Selected Border
	SpriteFrame(-29, -22, 56, 21, 223, 679) // #22 - Exit
	SpriteFrame(20, 16, 8, 13, 330, 604) // #23 - Lock

	arrayPos0 = 32
	while arrayPos0 < 1056
		if object[arrayPos0].type == TypeName[Round Icon]
			object[arrayPos0].frame = Object.Portrait[arrayPos0]
			object[arrayPos0].frame += 11
			object[arrayPos0].rotation = Object.Portrait[arrayPos0]
			object[arrayPos0].rotation <<= 8
			if Object.Portrait[arrayPos0] > 3
				object[arrayPos0].rotation += 256
			end if
		end if
		arrayPos0++
	loop

	scene3D.vertexCount = 4
	scene3D.faceCount = 1
	faceBuffer[0].a = 0
	faceBuffer[0].b = 1
	faceBuffer[0].c = 2
	faceBuffer[0].d = 3
	faceBuffer[0].flag = FACE_FLAG_TEXTURED_2D
end event


// ========================
// Editor Subs
// ========================




// Property Value



// roundID






// Property Value



// roundID








event RSDKDraw
	object.frame = Object.Portrait
	object.frame += 11

	DrawSprite(10)

	if Object.Portrait < 7 // Draw for all main stages
		DrawSprite(19)
	else // Draw the Exit
		DrawSprite(20)
		DrawSprite(22)
	end if

	DrawSprite(object.frame)
end event


event RSDKLoad
	LoadSpriteSheet("TAttack/TimeAttack.gif")
	SpriteFrame(0, 0, 6, 8, 330, 577) // #0  - 0
	SpriteFrame(0, 0, 6, 8, 337, 577) // #1  - 1
	SpriteFrame(0, 0, 6, 8, 344, 577) // #2  - 2
	SpriteFrame(0, 0, 6, 8, 351, 577) // #3  - 3
	SpriteFrame(0, 0, 6, 8, 330, 586) // #4  - 4
	SpriteFrame(0, 0, 6, 8, 337, 586) // #5  - 5
	SpriteFrame(0, 0, 6, 8, 344, 586) // #6  - 6
	SpriteFrame(0, 0, 6, 8, 351, 586) // #7  - 7
	SpriteFrame(0, 0, 6, 8, 330, 595) // #8  - 8
	SpriteFrame(0, 0, 6, 8, 337, 595) // #9  - 9
	SpriteFrame(-34, -45, 68, 90, 1, 609) // #10 - Border
	SpriteFrame(-32, 15, 64, 16, 70, 577) // #11 - Palmtree Panic
	SpriteFrame(-32, 15, 64, 16, 135, 577) // #12 - Collision Chaos
	SpriteFrame(-32, 15, 64, 16, 200, 577) // #13 - Tidal Tempest
	SpriteFrame(-32, 15, 64, 16, 265, 577) // #14 - Quartz Quadrant
	SpriteFrame(-32, 15, 64, 16, 70, 593) // #15 - Wacky Workbench
	SpriteFrame(-32, 15, 64, 16, 135, 593) // #16 - Stardust Speedway
	SpriteFrame(-32, 15, 64, 16, 200, 593) // #17 - Metallic Madness
	SpriteFrame(-32, 15, 64, 16, 265, 593) // #18 - Total Time
	SpriteFrame(-32, 31, 64, 13, 70, 638) // #19 - ' "
	SpriteFrame(-32, 31, 64, 13, 70, 652) // #20 -  ' "
	SpriteFrame(-35, -46, 70, 92, 441, 860) // #21 - Selected Border
	SpriteFrame(-29, -22, 56, 21, 223, 679) // #22 - Exit
	SpriteFrame(20, 16, 8, 13, 330, 604) // #23 - Lock











end event
