//-----------------Sonic CD BGEffects Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
// Values 0-1 are unused...
private alias Object.Value2 : Object.PrevAngle
private alias Object.Value3 : Object.FansTimer
private alias Object.Value4 : Object.CrunchersTimer
private alias Object.Value5 : Object.FansSprite
private alias Object.Value6 : Object.CrunchersSprite
private alias Object.Value7 : Object.ActivePalette

// Priority
private alias 1 : PRIORITY_ACTIVE


event ObjectDraw
	// Animate the Fans in the level
	Object.FansTimer++
	if Object.FansTimer > 2
		Object.FansTimer = 0
		Object.FansSprite++
		Object.FansSprite %= 3
		switch Object.FansSprite
		case 0
			// Fans are 4x4 tiles, comprised of 8 unique tiles, so
			// 8 individual tiles need to be updated to change the entire sprite
			Copy16x16Tile(296, 520)
			Copy16x16Tile(297, 521)
			Copy16x16Tile(298, 522)
			Copy16x16Tile(299, 523)
			Copy16x16Tile(300, 524)
			Copy16x16Tile(301, 525)
			Copy16x16Tile(302, 526)
			Copy16x16Tile(303, 527)
			break
			
		case 1
			Copy16x16Tile(296, 528)
			Copy16x16Tile(297, 529)
			Copy16x16Tile(298, 530)
			Copy16x16Tile(299, 531)
			Copy16x16Tile(300, 532)
			Copy16x16Tile(301, 533)
			Copy16x16Tile(302, 534)
			Copy16x16Tile(303, 535)
			break
			
		case 2
			Copy16x16Tile(296, 536)
			Copy16x16Tile(297, 537)
			Copy16x16Tile(298, 538)
			Copy16x16Tile(299, 539)
			Copy16x16Tile(300, 540)
			Copy16x16Tile(301, 541)
			Copy16x16Tile(302, 542)
			Copy16x16Tile(303, 543)
			break
			
		end switch
	end if
	
	// Animate the Crunchers in the level
	Object.CrunchersTimer++
	if Object.CrunchersTimer > 5
		Object.CrunchersTimer = 0
		Object.CrunchersSprite++
		Object.CrunchersSprite &= 3
		switch Object.CrunchersSprite
		case 0
			// Crunchers are 2x2 tiles, so 4 tiles need to be updated
			// in order to completely change their sprite
			Copy16x16Tile(189, 544)
			Copy16x16Tile(190, 545)
			Copy16x16Tile(191, 548)
			Copy16x16Tile(192, 549)
			break
			
		case 1
			Copy16x16Tile(189, 546)
			Copy16x16Tile(190, 547)
			Copy16x16Tile(191, 550)
			Copy16x16Tile(192, 551)
			break
			
		case 2
			Copy16x16Tile(189, 552)
			Copy16x16Tile(190, 553)
			Copy16x16Tile(191, 556)
			Copy16x16Tile(192, 557)
			break
			
		case 3
			Copy16x16Tile(189, 554)
			Copy16x16Tile(190, 555)
			Copy16x16Tile(191, 558)
			Copy16x16Tile(192, 559)
			break
			
		end switch
	end if
	
	// Handle the palette animation
	Object.AnimationTimer++
	if Object.AnimationTimer > 7
		Object.AnimationTimer = 0
		Object.ActivePalette++
		Object.ActivePalette &= 3
		RotatePalette(0, 202, 205, true)
	end if
	
	// Find the difference between the current scroll pos and last scroll pos
	temp1 = Object.PrevAngle
	temp2 = TileLayer[0].Angle
	
	if temp1 < temp2
		temp3 = temp2
		temp3 -= temp1
		if temp3 > 256
			// Account for looping
			temp1 += 512
		end if
	else
		temp3 = temp1
		temp3 -= temp2
		if temp3 > 256
			// Account for looping
			temp2 += 512
		end if
	end if
	
	temp0 = temp1
	temp0 -= temp2
	
	// Store the current angle for calculations next frame
	Object.PrevAngle = TileLayer[0].Angle
	
	// Move the other BG layers in accordance with the difference found
	
	temp1 = 0x40000
	temp2 = 0x30000
	temp3 = 0x10000
	temp4 = 0x20000
	
	temp1 *= temp0
	temp2 *= temp0
	temp3 *= temp0
	temp4 *= temp0
	
	HParallax[1].ScrollPos += temp1
	HParallax[2].ScrollPos += temp2
	HParallax[3].ScrollPos += temp3
	HParallax[4].ScrollPos += temp4
	
end event


event ObjectStartup
	
	// Set stage bounds
	Stage.curXBoundary1 = 0x3400000 // 832 pixels to the left
	Stage.curXBoundary2 = 0xCC00000 // 3264 pixels to the right
	Stage.curYBoundary1 = 0x3400000 // 832 pixels upwards
	Stage.curYBoundary2 = 0xCC00000 // 3264 pixels downwards
	
#platform: HW_Rendering
	// Load the map sheet if needed for HW rendering
	
	LoadSpriteSheet("Special/SSMap3.gif")
#endplatform
	
	// Setup all BGEffects in the level
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[BGEffects]
			
			// Set the object to always active
			Object[ArrayPos0].Priority = PRIORITY_ACTIVE
			
			// Make the object among the first to have its draw code be run
			Object[ArrayPos0].DrawOrder = 0
			
			// Set the starting angle
			// (Assuming Sonic starts facing left)
			Object[ArrayPos0].PrevAngle = 368
			
		end if
		
		ArrayPos0++
	loop
	
	// Setup the alternate palettes
	//temp0 = 1
	//while temp0 < 4
		// Shift the palette a bit between loops
		//RotatePalette(202, 205, true)
		//CopyPalette(0, temp0)
		//temp0++
	//loop
	
	// And shift it one last time, to restore the palette to what it was originally
	RotatePalette(202, 205, true)
	
end event


// ========================
// Editor Subs
// ========================

sub RSDKDraw
	DrawSprite(0)
end sub


sub RSDKLoad
	LoadSpriteSheet("Special/SSMap3.gif")
	SpriteFrame(-256, -256, 512, 512, 0, 0)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end sub
