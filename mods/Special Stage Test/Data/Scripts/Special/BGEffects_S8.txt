//-----------------Sonic CD BGEffects Script------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
// Values 0-1 are unused...
private alias Object.Value2 : Object.PrevAngle
private alias Object.Value3 : Object.FansTimer
private alias Object.Value4 : Object.CrunchersTimer
private alias Object.Value5 : Object.FansSprite
private alias Object.Value6 : Object.CrunchersSprite

// Priority
private alias 1 : PRIORITY_ACTIVE


event ObjectDraw
	// Animate the fans that are in the level
	Object.FansTimer++
	if Object.FansTimer > 2
		Object.FansTimer = 0
		Object.FansSprite++
		Object.FansSprite %= 3
		switch Object.FansSprite
		case 0
			// Fans are 4x4 tiles, comprised of 8 unique tiles, so
			// 8 tiles need to be updated to change the entire sprite
			Copy16x16Tile(237, 308)
			Copy16x16Tile(238, 309)
			Copy16x16Tile(239, 310)
			Copy16x16Tile(240, 311)
			Copy16x16Tile(241, 312)
			Copy16x16Tile(242, 313)
			Copy16x16Tile(243, 314)
			Copy16x16Tile(244, 315)
			break
			
		case 1
			Copy16x16Tile(237, 316)
			Copy16x16Tile(238, 317)
			Copy16x16Tile(239, 318)
			Copy16x16Tile(240, 319)
			Copy16x16Tile(241, 320)
			Copy16x16Tile(242, 321)
			Copy16x16Tile(243, 322)
			Copy16x16Tile(244, 323)
			break
			
		case 2
			Copy16x16Tile(237, 324)
			Copy16x16Tile(238, 325)
			Copy16x16Tile(239, 326)
			Copy16x16Tile(240, 327)
			Copy16x16Tile(241, 328)
			Copy16x16Tile(242, 329)
			Copy16x16Tile(243, 330)
			Copy16x16Tile(244, 331)
			break
			
		end switch
	end if
	
	// Animate the hungry crunchers throughout the level, too
	Object.CrunchersTimer++
	if Object.CrunchersTimer > 5
		Object.CrunchersTimer = 0
		Object.CrunchersSprite++
		Object.CrunchersSprite &= 3
		switch Object.CrunchersSprite
		case 0
			// Crunchers are 2x2 tiles, so 4 tiles need to be updated
			// in order to completely change their sprite
			Copy16x16Tile(135, 332)
			Copy16x16Tile(136, 333)
			Copy16x16Tile(137, 336)
			Copy16x16Tile(138, 337)
			break
			
		case 1
			Copy16x16Tile(135, 334)
			Copy16x16Tile(136, 335)
			Copy16x16Tile(137, 338)
			Copy16x16Tile(138, 339)
			break
			
		case 2
			Copy16x16Tile(135, 340)
			Copy16x16Tile(136, 341)
			Copy16x16Tile(137, 344)
			Copy16x16Tile(138, 345)
			break
			
		case 3
			Copy16x16Tile(135, 342)
			Copy16x16Tile(136, 343)
			Copy16x16Tile(137, 346)
			Copy16x16Tile(138, 347)
			break
			
		end switch
	end if
	
	// Find the difference between the current scroll position and the last scroll position... but nothing's really done with the result
	// This script was likely just copied over from the other BGEffects scripts, and this was never removed
	temp1 = Object.PrevAngle
	temp2 = TileLayer[0].Angle
	if temp1 < temp2
		temp3 = temp2
		temp3 -= temp1
		if temp3 > 256
			temp1 += 512
		end if
	else
		temp3 = temp1
		temp3 -= temp2
		if temp3 > 256
			temp2 += 512
		end if
	end if
	
	// Object.PrevAngle would normally be updated here, it looks like at least *some* effort was taken to remove the useless code
	
end event


event ObjectStartup
	
	// Setup stage bounds
	// Interestingly, this stage has different widths and heights, not making it a square like the others
	Stage.curXBoundary1 = 0x2800000 // left bounds at 640 pixels
	Stage.curXBoundary2 = 0xD800000 // right bounds at 3456 pixels
	Stage.curYBoundary1 = 0x3000000 // upper bounds at 768 pixels
	Stage.curYBoundary2 = 0xD000000 // lower bounds at 3328 pixels
	
#platform: HW_Rendering
	// Load the stage map as a sprite if needed
	
	LoadSpriteSheet("Special/SSMap8.gif")
#endplatform
	
	// Center the stage's background so that Robotnik's amazing mug can be in the middle of the screen
	temp0 = Screen.XSize
	temp0 -= 320
	
	// Shouldn't this be <<= 16? If it's going from screen-space to world-space...
	temp0 <<= 15
	
	// Base position of 96 pixels
	HParallax[0].ScrollPos = 0x600000
	
	// And then shift it as needed to match up centre screen
	HParallax[0].ScrollPos -= temp0
	
	// Cycle through all BGEffects objects and set them up
	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[BGEffects]
			
			// Make the object always be run, since fans and tiles should be animated always
			Object[ArrayPos0].Priority = PRIORITY_ACTIVE
			
			// Give it the lowest draw order as well, in order to make it run before other objects' draw events
			Object[ArrayPos0].DrawOrder = 0
			
			// Set the initial starting angle, although it's not really used...
			Object[ArrayPos0].PrevAngle = 368
			
		end if
		
		ArrayPos0++
	loop
	
end event


// ========================
// Editor events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Special/SSMap8.gif")
	SpriteFrame(-256, -256, 512, 512, 0, 0)

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
